title Furniture Science 0.9.87

author vexorian
homepage vexorian.com


background_color #000000
text_color #BEA03B

(debug
verbose_logging)
again_interval 0.03
(again_interval 1.0)
run_rules_on_level_start
realtime_interval 0.25

level_select
mouse_left MouseButton:left
mouse_right MouseButton:right

(key_repeat_interval 0.15)
norepeat_action
(
zlib License

Copyright (c) 2021, Victor Hugo Soliz Kuncar

This software is provided 'as-is', without any express or implied
warranty. In no event will the authors be held liable for any damages
arising from the use of this software.

Permission is granted to anyone to use this software for any purpose,
including commercial applications, and to alter it and redistribute it
freely, subject to the following restrictions:

1. The origin of this software must not be misrepresented; you must not
   claim that you wrote the original software. If you use this software
   in a product, an acknowledgment in the product documentation would be
   appreciated but is not required.
2. Altered source versions must be plainly marked as such, and must not be
   misrepresented as being the original software.
3. This notice may not be removed or altered from any source distribution.

)


( Some comments here


1. I really wanted to make logic for when you push a portal against a block, it would teleport the block through the other portal. But this
has proven really difficult for puzzle script, so for now pushing portals towards other objects won't allow them to push


)


=======
TAGS
=======
color          = yellow white
portal_color   = blue orange
weapon_status  = blue orange disarmed
uu = _U __
dd = _D __
rr = _R __
ll = _L __

vertical_tag   = down up
horizontal_tag = left right
directions_tag = up down left right
player_face    = up down left right front

smod3 = s0 s1 s2

=========
MAPPINGS
=========

portal_color => toggle_portal_color
blue orange  -> orange blue

directions         => rotate_directions_cw
up right down left -> right down left up

directions         => rotate_directions_cnw
up left down right -> left down right up

vertical_tag => flip_vertical
up down      -> down up

horizontal_tag => flip_horizontal
left right     -> right left

directions         => opposite_directions
up right down left -> down left up right


========
OBJECTS
========

Background
#333333

GroundTop
#3F3F3F
00000
.....
.....
.....
.....

GroundLeft
#3F3F3F
0....
0....
0....
0....
0....

GroundRight
#292929
....0
....0
....0
....0
....0

GroundBottom
#292929
.....
.....
.....
.....
00000



Wall
#c8c8c8 #efefef #b0b0b0 #ffffff
00000
00000
00000
00000
00000


WallOdd
#c8c8c8 #efefef #b0b0b0 #ffffff
22222
22222
22222
22222
22222

(#d8d8d8 #efefef #a0a0a0
11111
10002
10002
10002
22222)

WallTop:white
#ffffff
00000
.....
.....
.....
.....

(transparent
#d8d8d8 #efefef #c0c0c0 #ffffff
33333
.222.
.....
.....
.....)

WallLeft:white
#ffffff
0....
0....
0....
0....
0....


(transparent
#d8d8d8 #efefef #c0c0c0 #ffffff
3....
32...
32...
32...
3....)

WallRight:white
#d8d8d8 #efefef #c0c0c0 #181818
....3
....3
....3
....3
....3

WallBottom:white
#d8d8d8 #efefef #c0c0c0 #181818
.....
.....
.....
.....
33333

WallDarkCorner:white
#181818
.....
.....
.....
.....
....0

Wallx1:white
transparent




DarkWall
#222222 #242424 #262626 #282828
00000
03010
00000
03020
00000

(
#222222 #242424 #262626 #282828
00331
00130
31301
03210
01232)

(DarkWallSprite1
#222222 #242424 #262626 #282828
30022
22311
31220
02012
22212

DarkWallSprite2
#222222 #242424 #262626 #282828
23201
20201
03123
10302
21222

DarkWallSprite3
#222222 #242424 #262626 #282828
01200
22020
11002
22223
01312)


DarkWallSprite1
#222222 #242424 #262626 #282828
00000
01020
00000
03010
00000

DarkWallSprite2
#222222 #242424 #262626 #282828
00000
02020
00000
03010
00000

DarkWallSprite3
#222222 #242424 #262626 #282828
00000
02030
00000
01030
00000

DarkWallSprite4
transparent

HintTerminal1
black #BEA03B
00000
01100
00010
01000
00000

HintTerminal2
black #BEA03B #CEB04B
00000
01100
00010
01200
00000


DarkWallTop
#454545
00000
.....
.....
.....
.....

DarkWallLeft
#454545
0....
0....
0....
0....
0....

DarkWallRight
#101010
....0
....0
....0
....0
....0

DarkWallBottom
#101010
.....
.....
.....
.....
00000

WallSeparatorD
#2B2B2B
.....
.....
.....
.....
00000

WallSeparatorU
#2B2B2B
00000
.....
.....
.....
.....

WallSeparatorL
#2B2B2B
0....
0....
0....
0....
0....



WallSeparatorR
#2B2B2B
....0
....0
....0
....0
....0



Portal1RS1
#1A4FDF #073CCF
....1
...01
...01
...01
....1
Portal:blue:right
#1541B7 #1D57F7
....1
...01
...01
...01
....1
Portal1RS2
transparent
Portal1RS3
#285BE6 #4B79F9
....1
...01
...01
...01
....1
Portal1RS4
transparent

Portal1LS1
#1A4FDF #073CCF
1....
10...
10...
10...
1....
Portal:blue:left
#1541B7 #1D57F7
1....
10...
10...
10...
1....
Portal1LS2
transparent
Portal1LS3
#285BE6 #4B79F9
1....
10...
10...
10...
1....
Portal1LS4
transparent

Portal1DS1
#1A4FDF #073CCF
.....
.....
.....
.000.
11111
Portal:blue:down
#1541B7 #1D57F7
.....
.....
.....
.000.
11111
Portal1DS2
transparent
Portal1DS3
#285BE6 #4B79F9
.....
.....
.....
.000.
11111
Portal1DS4
transparent

Portal1US1
#1A4FDF #073CCF
11111
.000.
.....
.....
.....
Portal:blue:up
#1541B7 #1D57F7
11111
.000.
.....
.....
.....
Portal1US2
transparent
Portal1US3
#285BE6 #4B79F9
11111
.000.
.....
.....
.....
Portal1US4
transparent


Portal2RS1
#DF6D1A #CF5B07
....1
...01
...01
...01
....1
Portal:orange:right
#B75915 #F7791D
....1
...01
...01
...01
....1
Portal2RS2
transparent
Portal2RS3
#E67828 #F9944B
....1
...01
...01
...01
....1
Portal2RS4
transparent

Portal2LS1
#DF6D1A #CF5B07
1....
10...
10...
10...
1....
Portal:orange:left
#B75915 #F7791D
1....
10...
10...
10...
1....
Portal2LS2
transparent
Portal2LS3
#E67828 #F9944B
1....
10...
10...
10...
1....
Portal2LS4
transparent

Portal2DS1
#DF6D1A #CF5B07
.....
.....
.....
.000.
11111

Portal:orange:down
#B75915 #F7791D
.....
.....
.....
.000.
11111

Portal2DS2
transparent

Portal2DS3
#E67828 #F9944B
.....
.....
.....
.000.
11111
Portal2DS4
transparent

Portal2US1
#DF6D1A #CF5B07
11111
.000.
.....
.....
.....
Portal:orange:up
#B75915 #F7791D
11111
.000.
.....
.....
.....
Portal2US2
transparent
Portal2US3
#E67828 #F9944B
11111
.000.
.....
.....
.....
Portal2US4
transparent





Target
#E8D138
.....
.000.
.0.0.
.000.
.....

TargetBorderTop
#E8D138
..0..
.....
.....
.....
.....
TargetBorderBottom
#E8D138
.....
.....
.....
.....
..0..

TargetBorderLeft
#E8D138
.....
.....
0....
.....
.....

TargetBorderRight
#E8D138
.....
.....
....0
.....
.....


Player:blue:front
black #DF60E2 #2A0B25 #45113D white blue
.000.
.010.
.054.
.000.
.3.3.

Player:blue:left
black #DF60E2 #2A0B25 #45113D white blue
..000
00010
54002
00002
..3.3


Player:blue:right
black #DF60E2 #2A0B25 #45113D white blue
000..
01000
20045
20000
3.3..

Player:blue:up
black #DF60E2 #2A0B25 #45113D white blue
00050
00040
0200.
020..
3.3..


Player:blue:down
black #DF60E2 #2A0B25 #45113D white blue
222..
000..
0100.
00040
3.050

Player:orange:front
black #DF60E2 #2A0B25 #45113D white Orange
.000.
.010.
.054.
.000.
.3.3.

Player:orange:left
black #DF60E2 #2A0B25 #45113D white Orange
..000
00010
54002
00002
..3.3


Player:orange:right
black #DF60E2 #2A0B25 #45113D white Orange
000..
01000
20045
20000
3.3..

Player:orange:up
black #DF60E2 #2A0B25 #45113D white Orange
00050
00040
0200.
020..
3.3..


Player:orange:down
black #DF60E2 #2A0B25 #45113D white Orange
222..
000..
0100.
00040
3.050






SinkingPlayerSprite1
#0F3318 #3DD162
.000.
.000.
.000.
.000.
.0.0.

SinkingPlayerSprite2
#0F3318 #3DD162
..0..
.000.
..0..
..00.
.0...

SinkingPlayerSprite3
#0F3318 #3DD162
..0..
.0.0.
.....
..0..
.0...

Player:disarmed:front
black #DF60E2 #2A0B25 #45113D
.000.
.010.
.000.
.000.
.3.3.



Player:disarmed:left
black #DF60E2 #2A0B25 #45113D
.000.
.010.
00002
.0002
.3.3.


Player:disarmed:right
black #DF60E2 #2A0B25 #45113D
.000.
.010.
20000
2000.
.3.3.


Player:disarmed:up
black #DF60E2 #2A0B25 #45113D
.000.
.000.
.020.
.020.
.3.3.


Player:disarmed:down
black #DF60E2 #2A0B25 #45113D
.222.
.000.
.010.
.000.
.3.3.


PortalGunTM
black blue orange white
..0.. 
0.1.0
.020.
.030.
..0..


Shot:blue:right
blue
.....
...0.
....0
...0.
.....

ParticleSprite1:blue:left
blue
.....
0....
.....
0....
.....

ParticleSprite2:blue:left
blue
0....
.....
.....
.....
0....


Shot:blue:left
blue
.....
.0...
0....
.0...
.....

ParticleSprite1:blue:right
blue
.....
....0
.....
....0
.....

ParticleSprite2:blue:right
blue
....0
.....
.....
.....
....0


Shot:blue:up
blue
..0..
.0.0.
.....
.....
.....


ParticleSprite1:blue:down
blue
.....
.....
.....
.....
.0.0.

ParticleSprite2:blue:down
blue
.....
.....
.....
.....
0...0


Shot:blue:down
blue
.....
.....
.....
.0.0.
..0..

ParticleSprite1:blue:up
blue
.0.0.
.....
.....
.....
.....

ParticleSprite2:blue:up
blue
0...0
.....
.....
.....
.....


ShotF:blue:right
blue
.....
...0.
....0
...0.
.....

ShotF:blue:left
blue
.....
.0...
0....
.0...
.....


ShotF:blue:up
blue
..0..
.0.0.
.....
.....
.....

ShotF:blue:down
blue
.....
.....
.....
.0.0.
..0..




Shot:orange:right
Orange
.....
...0.
....0
...0.
.....

ParticleSprite1:orange:left
Orange
.....
0....
.....
0....
.....

ParticleSprite2:orange:left
Orange
0....
.....
.....
.....
0....


Shot:orange:left
Orange
.....
.0...
0....
.0...
.....

ParticleSprite1:orange:right
Orange
.....
....0
.....
....0
.....

ParticleSprite2:orange:right
Orange
....0
.....
.....
.....
....0


Shot:orange:up
Orange
..0..
.0.0.
.....
.....
.....


ParticleSprite1:orange:down
Orange
.....
.....
.....
.....
.0.0.

ParticleSprite2:orange:down
Orange
.....
.....
.....
.....
0...0


Shot:orange:down
Orange
.....
.....
.....
.0.0.
..0..

ParticleSprite1:orange:up
Orange
.0.0.
.....
.....
.....
.....

ParticleSprite2:orange:up
Orange
0...0
.....
.....
.....
.....


ShotF:orange:right
Orange
.....
...0.
....0
...0.
.....

ShotF:orange:left
Orange
.....
.0...
0....
.0...
.....


ShotF:orange:up
Orange
..0..
.0.0.
.....
.....
.....

ShotF:orange:down
Orange
.....
.....
.....
.0.0.
..0..







Water
#1D7C6B #83867A
00000
10000
00000
00010
00000

WaterSprite1
#1D7C6B #83867A
00000
00100
00000
01000
00000

WaterSprite2
#1D7C6B #83867A
00000
00010
00000
00100
00000

WaterSprite3
#1D7C6B #83867A
00010
00000
00000
01000
00000

WaterSprite4
transparent

Block:__:__:__:__:yellow
#E5D98B #D4BF33 #1F1B07 #B6A42C #E8D138 transparent
50005
01412
04342
01412
52225

Block:__:__:__:_R:yellow
#E5D98B #D4BF33 #1F1B07 #B6A42C #E8D138 transparent
50000
01411
04341
01411
52222


Block:__:__:_L:__:yellow
#E5D98B #D4BF33 #1F1B07 #B6A42C #E8D138 transparent
00005
11412
14342
11412
22225

Block:__:__:_L:_R:yellow
#E5D98B #D4BF33 #1F1B07 #B6A42C #E8D138 transparent
00000
11411
14341
11411
22222


Block:__:_D:__:__:yellow
#E5D98B #D4BF33 #1F1B07 #B6A42C #E8D138 transparent
50005
01412
04342
01412
01112


Block:__:_D:__:_R:yellow
#E5D98B #D4BF33 #1F1B07 #B6A42C #E8D138 transparent
50000
01411
04341
01411
01112


Block:__:_D:_L:__:yellow
#E5D98B #D4BF33 #1F1B07 #B6A42C #E8D138 transparent
00005
11412
14342
11412
11112


Block:__:_D:_L:_R:yellow
#E5D98B #D4BF33 #1F1B07 #B6A42C #E8D138 transparent
00000
11411
14341
11411
11112

Block:_U:__:__:__:yellow
#E5D98B #D4BF33 #1F1B07 #B6A42C #E8D138 transparent
01112
01412
04342
01412
52225

Block:_U:__:__:_R:yellow
#E5D98B #D4BF33 #1F1B07 #B6A42C #E8D138 transparent
01112
01411
04341
01411
52222

Block:_U:__:_L:__:yellow
#E5D98B #D4BF33 #1F1B07 #B6A42C #E8D138 transparent
01112
11412
14342
11412
22225


Block:_U:__:_L:_R:yellow
#E5D98B #D4BF33 #1F1B07 #B6A42C #E8D138 transparent
01112
11411
14341
11411
22222

Block:_U:_D:__:__:yellow
#E5D98B #D4BF33 #1F1B07 #B6A42C #E8D138 transparent
01112
01412
04342
01412
01112


Block:_U:_D:__:_R:yellow
#E5D98B #D4BF33 #1F1B07 #B6A42C #E8D138 transparent
01112
01411
04341
01411
01112

Block:_U:_D:_L:__:yellow
#E5D98B #D4BF33 #1F1B07 #B6A42C #E8D138 transparent
01112
11412
14342
11412
11112



Block:_U:_D:_L:_R:yellow
#E5D98B #D4BF33 #1F1B07 #B6A42C #E8D138
21110
11411
14341
11411
01112


BlockCorner:up:right:yellow
#D4BF33
....0
.....
.....
.....
.....

BlockCorner:up:left:yellow
#D4BF33
0....
.....
.....
.....
.....

BlockCorner:down:left:yellow
#D4BF33
.....
.....
.....
.....
0....

BlockCorner:down:right:yellow
#D4BF33
.....
.....
.....
.....
....0

BlockStub:uu:dd:ll:rr:yellow
#E5D98B #D4BF33 #1F1B07 #B6A42C #E8D138
1.1.1
.111.
11341
.141.
1.1.1


BlockStub:uu:dd:ll:rr:white
#FFFFFF #e0e0e0 #232323 #e0e0e0 #e0e0e0
1.1.1
.111.
11341
.141.
1.1.1



Block:__:__:__:__:white
#FFFFFF #e0e0e0 #232323 #e0e0e0 #e0e0e0 transparent
50005
03332
03332
03332
52225

Block:__:__:__:_R:white
#FFFFFF #e0e0e0 #232323 #e0e0e0 #e0e0e0 transparent
50000
01411
04341
01411
52222


Block:__:__:_L:__:white
#FFFFFF #e0e0e0 #232323 #e0e0e0 #e0e0e0 transparent
00005
11412
14342
11412
22225

Block:__:__:_L:_R:white
#FFFFFF #e0e0e0 #232323 #e0e0e0 #e0e0e0 transparent
00000
11411
14341
11411
22222


Block:__:_D:__:__:white
#FFFFFF #e0e0e0 #232323 #e0e0e0 #e0e0e0 transparent
50005
01412
04342
01412
01112


Block:__:_D:__:_R:white
#FFFFFF #e0e0e0 #232323 #e0e0e0 #e0e0e0 transparent
50000
01411
04341
01411
01112


Block:__:_D:_L:__:white
#FFFFFF #e0e0e0 #232323 #e0e0e0 #e0e0e0 transparent
00005
11412
14342
11412
11112


Block:__:_D:_L:_R:white
#FFFFFF #e0e0e0 #232323 #e0e0e0 #e0e0e0 transparent
00000
11411
14341
11411
11112

Block:_U:__:__:__:white
#FFFFFF #e0e0e0 #232323 #e0e0e0 #e0e0e0 transparent
01112
01412
04342
01412
52225

Block:_U:__:__:_R:white
#FFFFFF #e0e0e0 #232323 #e0e0e0 #e0e0e0 transparent
01112
01411
04341
01411
52222

Block:_U:__:_L:__:white
#FFFFFF #e0e0e0 #232323 #e0e0e0 #e0e0e0 transparent
01112
11412
14342
11412
22225


Block:_U:__:_L:_R:white
#FFFFFF #e0e0e0 #232323 #e0e0e0 #e0e0e0 transparent
01112
11411
14341
11411
22222

Block:_U:_D:__:__:white
#FFFFFF #e0e0e0 #232323 #e0e0e0 #e0e0e0 transparent
01112
01412
04342
01412
01112


Block:_U:_D:__:_R:white
#FFFFFF #e0e0e0 #232323 #e0e0e0 #e0e0e0 transparent
01112
01411
04341
01411
01112

Block:_U:_D:_L:__:white
#FFFFFF #e0e0e0 #232323 #e0e0e0 #e0e0e0 transparent
01112
11412
14342
11412
11112



Block:_U:_D:_L:_R:white
#FFFFFF #e0e0e0 #232323 #e0e0e0 #e0e0e0 transparent
21110
11411
14341
11411
01112


BlockCorner:up:right:white
#e0e0e0
....0
.....
.....
.....
.....

BlockCorner:up:left:white
#e0e0e0
0....
.....
.....
.....
.....

BlockCorner:down:left:white
#e0e0e0
.....
.....
.....
.....
0....

BlockCorner:down:right:white
#e0e0e0
.....
.....
.....
.....
....0

Bridge:__:__:__:__:white
#1B7162 #72F1DA #1B7162 #72F1DA #72F1DA #1D7C6B
copy:Block:__:__:__:__:white

Bridge:__:__:__:_R:white
#1B7162 #72F1DA #1B7162 #72F1DA #72F1DA #1D7C6B
copy:Block:__:__:__:_R:white

Bridge:__:__:_L:__:white
#1B7162 #72F1DA #1B7162 #72F1DA #72F1DA #1D7C6B
copy:Block:__:__:_L:__:white

Bridge:__:__:_L:_R:white
#1B7162 #72F1DA #1B7162 #72F1DA #72F1DA #1D7C6B
copy:Block:__:__:_L:_R:white

Bridge:__:_D:__:__:white
#1B7162 #72F1DA #1B7162 #72F1DA #72F1DA #1D7C6B
copy:Block:__:_D:__:__:white

Bridge:__:_D:__:_R:white
#1B7162 #72F1DA #1B7162 #72F1DA #72F1DA #1D7C6B
copy:Block:__:_D:__:_R:white

Bridge:__:_D:_L:__:white
#1B7162 #72F1DA #1B7162 #72F1DA #72F1DA #1D7C6B
copy:Block:__:_D:_L:__:white

Bridge:__:_D:_L:_R:white
#1B7162 #72F1DA #1B7162 #72F1DA #72F1DA #1D7C6B
copy:Block:__:_D:_L:_R:white

Bridge:_U:__:__:__:white
#1B7162 #72F1DA #1B7162 #72F1DA #72F1DA #1D7C6B
copy:Block:_U:__:__:__:white

Bridge:_U:__:__:_R:white
#1B7162 #72F1DA #1B7162 #72F1DA #72F1DA #1D7C6B
copy:Block:_U:__:__:_R:white

Bridge:_U:__:_L:__:white
#1B7162 #72F1DA #1B7162 #72F1DA #72F1DA #1D7C6B
copy:Block:_U:__:_L:__:white

Bridge:_U:__:_L:_R:white
#1B7162 #72F1DA #1B7162 #72F1DA #72F1DA #1D7C6B
copy:Block:_U:__:_L:_R:white

Bridge:_U:_D:__:__:white
#1B7162 #72F1DA #1B7162 #72F1DA #72F1DA #1D7C6B
copy:Block:_U:_D:__:__:white

Bridge:_U:_D:__:_R:white
#1B7162 #72F1DA #1B7162 #72F1DA #72F1DA #1D7C6B
copy:Block:_U:_D:__:_R:white

Bridge:_U:_D:_L:__:white
#1B7162 #72F1DA #1B7162 #72F1DA #72F1DA #1D7C6B
copy:Block:_U:_D:_L:__:white

Bridge:_U:_D:_L:_R:white
#1B7162 #72F1DA #1B7162 #72F1DA #72F1DA #1D7C6B
copy:Block:_U:_D:_L:_R:white

BridgeCorner:up:right:white
#72F1DA
copy:BlockCorner:up:right:yellow

BridgeCorner:up:left:white
#72F1DA
copy:BlockCorner:up:left:yellow

BridgeCorner:down:right:white
#72F1DA
copy:BlockCorner:down:right:yellow

BridgeCorner:down:left:white
#72F1DA
copy:BlockCorner:down:left:yellow

Bridge:__:__:__:__:yellow
#1B7162 #38E864 #1B7162 #34D95D #4EE874 #1D7C6B
copy:Block:__:__:__:__:yellow

Bridge:__:__:__:_R:yellow
#1B7162 #38E864 #1B7162 #34D95D #4EE874 #1D7C6B
copy:Block:__:__:__:_R:yellow

Bridge:__:__:_L:__:yellow
#1B7162 #38E864 #1B7162 #34D95D #4EE874 #1D7C6B
copy:Block:__:__:_L:__:yellow

Bridge:__:__:_L:_R:yellow
#1B7162 #38E864 #1B7162 #34D95D #4EE874 #1D7C6B
copy:Block:__:__:_L:_R:yellow

Bridge:__:_D:__:__:yellow
#1B7162 #38E864 #1B7162 #34D95D #4EE874 #1D7C6B
copy:Block:__:_D:__:__:yellow

Bridge:__:_D:__:_R:yellow
#1B7162 #38E864 #1B7162 #34D95D #4EE874 #1D7C6B
copy:Block:__:_D:__:_R:yellow

Bridge:__:_D:_L:__:yellow
#1B7162 #38E864 #1B7162 #34D95D #4EE874 #1D7C6B
copy:Block:__:_D:_L:__:yellow

Bridge:__:_D:_L:_R:yellow
#1B7162 #38E864 #1B7162 #34D95D #4EE874 #1D7C6B
copy:Block:__:_D:_L:_R:yellow

Bridge:_U:__:__:__:yellow
#1B7162 #38E864 #1B7162 #34D95D #4EE874 #1D7C6B
copy:Block:_U:__:__:__:yellow

Bridge:_U:__:__:_R:yellow
#1B7162 #38E864 #1B7162 #34D95D #4EE874 #1D7C6B
copy:Block:_U:__:__:_R:yellow

Bridge:_U:__:_L:__:yellow
#1B7162 #38E864 #1B7162 #34D95D #4EE874 #1D7C6B
copy:Block:_U:__:_L:__:yellow

Bridge:_U:__:_L:_R:yellow
#1B7162 #38E864 #1B7162 #34D95D #4EE874 #1D7C6B
copy:Block:_U:__:_L:_R:yellow

Bridge:_U:_D:__:__:yellow
#1B7162 #38E864 #1B7162 #34D95D #4EE874 #1D7C6B
copy:Block:_U:_D:__:__:yellow

Bridge:_U:_D:__:_R:yellow
#1B7162 #38E864 #1B7162 #34D95D #4EE874 #1D7C6B
copy:Block:_U:_D:__:_R:yellow

Bridge:_U:_D:_L:__:yellow
#1B7162 #38E864 #1B7162 #34D95D #4EE874 #1D7C6B
copy:Block:_U:_D:_L:__:yellow

Bridge:_U:_D:_L:_R:yellow
#1B7162 #38E864 #1B7162 #34D95D #4EE874 #1D7C6B
copy:Block:_U:_D:_L:_R:yellow

BridgeCorner:up:right:yellow
#38E864
copy:BlockCorner:up:right:yellow

BridgeCorner:up:left:yellow
#38E864
copy:BlockCorner:up:left:yellow

BridgeCorner:down:right:yellow
#38E864
copy:BlockCorner:down:right:yellow

BridgeCorner:down:left:yellow
#38E864
copy:BlockCorner:down:left:yellow

Want:left
red blue
.....
.....
.01..
.....
.....


Want:right
red blue
.....
.....
..10.
.....
.....


Want:up
red blue
.....
..0..
..1..
.....
.....


Want:down
red blue
.....
.....
..1..
..0..
.....


Error
black
.....
.0.0.
..0..
.0.0.
.....

PostError
red
.....
..0..
.0.0.
..0..
.....


(

WantL
transparent

WantR
transparent

WantU
transparent

WantD
transparent

Error
transparent

PostError
transparent


)

MightSink
transparent


Origin:directions
transparent

PortalExit:portal_color:directions
transparent


ActivePortal:directions
transparent


RotateRR
transparent

RotateRL
transparent

RotateRU
transparent

RotateRD
transparent

RotateLR
transparent

RotateLL
transparent

RotateLU
transparent

RotateLD
transparent

RotateUR
transparent

RotateUL
transparent

RotateUU
transparent 

RotateUD
transparent

RotateDR
transparent

RotateDL
transparent

RotateDU
transparent

RotateDD
transparent




OkRigid:directions
transparent

RigidEnteringPortal1
transparent

RigidLeavingPortal1
transparent

RigidEnteringPortal2
transparent

RigidLeavingPortal2
transparent


BPEnteringPortal1
transparent

BPEnteringPortal2
transparent

BPLeavingPortal1
transparent

BPLeavingPortal2
transparent

NoPortal1
transparent

NoPortal2
transparent

ColorToggle
transparent

MouseButton:horizontal
transparent


GotGun
transparent


HasCellU
transparent

HasCellD
transparent

HasCellL
transparent

HasCellR
transparent

RealTimeTick
transparent

U0
#424242 #333333
11111
11111
11111
10001
10101

D0
#424242 #333333
10101
10101
10001
11111
11111

U1
#424242 #333333
11111
11111
11111
11011
10011

D1
#424242 #333333
11011
11011
10001
11111
11111

U2
#424242 #333333
11111
11111
11111
10001
11101

D2
#424242 #333333
10001
10111
10001
11111
11111

D3
#424242 #333333
10001
11101
10001
11111
11111

U4
#424242 #333333
11111
11111
11111
10101
10101

D4
#424242 #333333
10001
11101
11101
11111
11111

U5
#424242 #333333
11111
11111
11111
10001
10111

D6
#424242 #333333
10001
10101
10001
11111
11111


D7
#424242 #333333
11101
11101
11101
11111
11111

B0
#424242
.000.
.0.0.
.0.0.
.0.0.
.000.

B1
#424242
...0.
...0.
...0.
...0.
...0.

B2
#424242
.000.
...0.
.000.
.0...
.000.

B3
#424242
.000.
...0.
.000.
...0.
.000.

B4
#424242
.0.0.
.0.0.
.000.
...0.
...0.


B5
#424242
.000.
.0...
.000.
...0.
.000.


B6
#424242
.000.
.0...
.000.
.0.0.
.000.


B7
#424242
.000.
...0.
...0.
...0.
...0.


B8
#424242
.000.
.0.0.
.000.
.0.0.
.000.

B9
#424242
.000.
.0.0.
.000.
...0.
.000.



PlayerNotFound
red
.....
.000.
.0.0.
.000.
.....

ThroughPortal:directions
transparent

FutureUsed
transparent

RigidNextToPortal:directions
transparent

UnmovablePortal
transparent

ErrorSprite
red
00.00
0...0
.....
0...0
00.00



ConveyorLeft
black
..0..
...0.
..0..
.0...
0....

JustTurned
transparent

Pointer
transparent

PointerEnd
transparent

EmptySpot
transparent

GoalSoundSprite
#F7DC43 yellow
.....
.000.
.010.
.000.
.....

CellOdd
transparent

CellEven
transparent


MissingPlayer
transparent

LightBridgeStarter:down
gray
00000
.....
.....
.....
.....

LightBridgeStarter:up
gray
.....
.....
.....
.....
00000

LightBridgeStarter:left
gray
....0
....0
....0
....0
....0

LightBridgeStarter:right
gray
0....
0....
0....
0....
0....

LightBridge:directions
transparent

LightBridgeSprite:s0:left
#A8006A #B80074 #E0008E #FF2EB3
.....
33333
01210
33333
.....

LightBridgeSprite:s1:left
#A8006A #B80074 #E0008E #FF29B1
.....
33333
12100
33333
.....

LightBridgeSprite:s2:left
#A8006A #B80074 #E0008E #FF33B5
.....
33333
10012
33333
.....

LightBridgeSprite:s0:right
#A8006A #B80074 #E0008E #FF33B5
copy:LightBridgeSprite:s0:left
flip:v

LightBridgeSprite:s1:right
#A8006A #B80074 #E0008E #FF33B5
copy:LightBridgeSprite:s1:left
flip:v

LightBridgeSprite:s2:right
#A8006A #B80074 #E0008E #FF33B5
copy:LightBridgeSprite:s2:left
flip:v


LightBridgeSprite:s0:down
#A8006A #B80074 #E0008E #FF33B5
copy:LightBridgeSprite:s0:left
rot:left:down

LightBridgeSprite:s1:down
#A8006A #B80074 #E0008E #FF33B5
copy:LightBridgeSprite:s1:left
rot:left:down

LightBridgeSprite:s2:down
#A8006A #B80074 #E0008E #FF33B5
copy:LightBridgeSprite:s2:left
rot:left:down

LightBridgeSprite:s0:up
#A8006A #B80074 #E0008E #FF33B5
copy:LightBridgeSprite:s0:left
rot:left:up

LightBridgeSprite:s1:up
#A8006A #B80074 #E0008E #FF33B5
copy:LightBridgeSprite:s1:left
rot:left:up

LightBridgeSprite:s2:up
#A8006A #B80074 #E0008E #FF33B5
copy:LightBridgeSprite:s2:left
rot:left:up


LightBridgeEnteringPortal:portal_color
transparent


=======
LEGEND
=======

. = Background
# = Wall
% = DarkWall
! = Player:blue:front and Background
¡ = Player:disarmed:front and Background

ñ = Block:__:__:__:__:yellow
⅄ = Block:__:__:__:_R:yellow
☥ = Block:__:__:_L:__:yellow
ⱬ = Block:__:__:_L:_R:yellow
┦ = Block:__:_D:__:__:yellow
⡧ = Block:__:_D:__:_R:yellow
ℌ = Block:__:_D:_L:__:yellow
╎ = Block:__:_D:_L:_R:yellow
⹏ = Block:_U:__:__:__:yellow
⸔ = Block:_U:__:__:_R:yellow
A = Block:_U:__:_L:__:yellow
B = Block:_U:__:_L:_R:yellow
C = Block:_U:_D:__:__:yellow
E = Block:_U:_D:_L:__:yellow
F = Block:_U:_D:_L:_R:yellow


} = Block:__:__:__:__:white
“ = Block:__:__:__:_R:white
» = Block:__:__:_L:__:white
· = Block:__:__:_L:_R:white
µ = Block:__:_D:__:__:white
½ = Block:__:_D:__:_R:white
¬ = Block:__:_D:_L:__:white
{ = Block:__:_D:_L:_R:white
¶ = Block:_U:__:__:__:white
\ = Block:_U:__:__:_R:white
æ = Block:_U:__:_L:__:white
” = Block:_U:__:_L:_R:white
¢ = Block:_U:_D:__:__:white
ð = Block:_U:_D:__:_R:white
€ = Block:_U:_D:_L:__:white
đ = Block:_U:_D:_L:_R:white


~ = Water

@ = Target
T = PortalGunTM

H = HintTerminal1 and DarkWall

0 = b0
1 = b1
2 = b2
3 = b3
4 = b4
5 = b5
6 = b6
7 = b7
8 = b8
9 = b9



_  = ConveyorLeft

↓ = LightBridgeStarter:down
« = LightBridgeStarter:up
ħ = LightBridgeStarter:right
ŧ = LightBridgeStarter:left


Y = BlockStub:_U:_D:_L:_R:yellow
W = BlockStub:_U:_D:_L:_R:white

HintTerminal = HintTerminal1 or HintTerminal2




BlockStubWithU:color = BlockStub:_U:dd:ll:rr:color
BlockStubWithD:color = BlockStub:uu:_D:ll:rr:color
BlockStubWithL:color = BlockStub:uu:dd:_L:rr:color
BlockStubWithR:color = BlockStub:uu:dd:ll:_R:color


Bridge:__:__:__:__ = Bridge:__:__:__:__:color
Bridge:__:__:__:_R = Bridge:__:__:__:_R:color
Bridge:__:__:_L:__ = Bridge:__:__:_L:__:color
Bridge:__:__:_L:_R = Bridge:__:__:_L:_R:color
Bridge:__:_D:__:__ = Bridge:__:_D:__:__:color
Bridge:__:_D:__:_R = Bridge:__:_D:__:_R:color
Bridge:__:_D:_L:__ = Bridge:__:_D:_L:__:color
Bridge:__:_D:_L:_R = Bridge:__:_D:_L:_R:color
Bridge:_U:__:__:__ = Bridge:_U:__:__:__:color
Bridge:_U:__:__:_R = Bridge:_U:__:__:_R:color
Bridge:_U:__:_L:__ = Bridge:_U:__:_L:__:color
Bridge:_U:__:_L:_R = Bridge:_U:__:_L:_R:color
Bridge:_U:_D:__:__ = Bridge:_U:_D:__:__:color
Bridge:_U:_D:__:_R = Bridge:_U:_D:__:_R:color
Bridge:_U:_D:_L:__ = Bridge:_U:_D:_L:__:color
Bridge:_U:_D:_L:_R = Bridge:_U:_D:_L:_R:color


Bridge = Bridge:uu:dd:ll:rr



Player:blue = Player:blue:front or Player:blue:directions

Player:orange = Player:orange:front or Player:orange:directions

Player:disarmed = Player:disarmed:front or Player:disarmed:directions




RealPlayer = Player:blue or Player:orange or Player:disarmed
Player = RealPlayer or MissingPlayer


With:left = Block:uu:dd:_L:rr:color or BlockStubWithL:color 
With:right = Block:__:__:__:_R:color or Block:__:__:_L:_R:color or Block:__:_D:__:_R:color or Block:__:_D:_L:_R:color or Block:_U:__:__:_R:color or Block:_U:__:_L:_R:color or Block:_U:_D:__:_R:color or Block:_U:_D:_L:_R:color or BlockStubWithR:color 
With:up = Block:_U:__:__:__:color or Block:_U:__:__:_R:color or Block:_U:__:_L:__:color or Block:_U:__:_L:_R:color or Block:_U:_D:__:__:color or Block:_U:_D:__:_R:color or Block:_U:_D:_L:__:color or Block:_U:_D:_L:_R:color or BlockStubWithU:color 
With:down = Block:__:_D:__:__:color or Block:__:_D:__:_R:color or Block:__:_D:_L:__:color  or Block:__:_D:_L:_R:color  or Block:_U:_D:__:__:color  or Block:_U:_D:__:_R:color  or Block:_U:_D:_L:__:color  or Block:_U:_D:_L:_R:color  or BlockStubWithD:color
 
 

 
Block = With:left or With:right or With:up or With:down or Block:__:__:__:__:color or BlockStub:__:__:__:__:color
WithCollision = Block or Player or Wall or DarkWall

BlockPlayer = Block or RealPlayer

WantL = Want:left
WantR = Want:right
WantD = Want:down
WantU = Want:up

Want = WantL or WantR or WantU or WantD
WantNotD = WantR or WantL or WantU
WantNotU = WantR or WantL          or WantD
WantNotR =          WantL or WantU or WantD
WantNotL = WantR          or WantU or WantD
WantH = WantL or WantR
WantV = WantD or WantU

OriginL = Origin:left
OriginR = Origin:right
OriginD = Origin:down
OriginU = Origin:up

Origin = OriginL or OriginR or OriginD or OriginU

Portal:blue   = Portal:blue:directions
Portal:orange = Portal:orange:directions

Portal = Portal:portal_color:directions


Portal:right = Portal:portal_color:right
Portal:left  = Portal:portal_color:left
Portal:up    = Portal:portal_color:up
Portal:down  = Portal:portal_color:down


PortalExit:blue   = PortalExit:blue:directions
PortalExit:orange = PortalExit:orange:directions

PortalExit = PortalExit:portal_color:directions

ActivePortal = ActivePortal:directions

RotateCW  = RotateLU or RotateUR or RotateRD or RotateDL
RotateCNW = RotateUL or RotateRU or RotateDR or RotateLD
RotatePerpendicular = RotateCW or RotateCNW
FlipVertical = RotateUU or RotateDD
FlipHorizontal = RotateLL or RotateRR
Rotate = RotateRR or RotateRL or RotateRU or RotateRD or RotateLR or RotateLL or RotateLU or RotateLD or RotateUR or RotateUL or RotateUU or RotateUD or RotateDR or RotateDL or RotateDU or RotateDD


OkRigid = OkRigid:directions

Portal1OrNothing = Portal:blue    or NoPortal1
Portal2OrNothing = Portal:orange  or NoPortal2

BlueShot = Shot:blue:directions
OrangeShot = Shot:orange:directions


Block:white = Block:uu:dd:ll:rr:white

WithWhite = Wall or Block:white

Player:front = Player:disarmed:front or Player:blue:front or Player:orange:front
WaterOrBridge = Water or Bridge
DarkWallSprite = DarkWallSprite1 or DarkWallSprite2 or DarkWallSprite3 or DarkWallSprite4
WaterSprite = WaterSprite1 or WaterSprite2 or WaterSprite3 or WaterSprite4

Portal1SPrite = Portal1RS1 or Portal1RS2 or Portal1RS3 or Portal1RS4 or Portal1LS1 or Portal1LS2 or Portal1LS3 or Portal1LS4 or Portal1US1 or Portal1US2 or Portal1US3 or Portal1US4 or Portal1DS1 or Portal1DS2 or Portal1DS3 or Portal1DS4
Portal2SPrite = Portal2RS1 or Portal2RS2 or Portal2RS3 or Portal2RS4 or Portal2LS1 or Portal2LS2 or Portal2LS3 or Portal2LS4 or Portal2US1 or Portal2US2 or Portal2US3 or Portal2US4 or Portal2DS1 or Portal2DS2 or Portal2DS3 or Portal2DS4

ThroughPortal = ThroughPortal:directions

WithVertical = With:up or With:down
WithHorizontal = With:left or With:right

RigidHNextToPortal = RigidNextToPortal:horizontal
RigidVNextToPortal = RigidNextToPortal:vertical
RigidNextToPortal  = RigidNextToPortal:directions

Player:left  = Player:weapon_status:left
Player:right = Player:weapon_status:right
Player:down  = Player:weapon_status:down
Player:up    = Player:weapon_status:up

DarkOrWhiteWall = Wall or DarkWall

LightBridge = LightBridge:directions
LightBridgeSprite:left = LightBridgeSprite:smod3:left
LightBridgeSprite:right = LightBridgeSprite:smod3:right
LightBridgeSprite:up = LightBridgeSprite:smod3:up
LightBridgeSprite:down = LightBridgeSprite:smod3:down



=======
SOUNDS
=======

SFX1 33053303:2 (portal gun)
sfx2 59657701:2 (shoot)
sfx3 57950702:2 (open portal)
sfx4 45276302:2 (teleport)
sfx5 24968106:2 (error)
sfx6 50609104:2 (sinkx)
sfx7 38799704:2 (player sink)
sfx8 22930307:2 (push block)
sfx9 47474505:2 (light error)
sfx10 57390700:2 (goal)
sfx11 86141708:2 (light bridge)

MouseButton:horizontal Destroy 31656906:10


================
COLLISIONLAYERS
================

Background
GroundTop
GroundLeft
GroundRight
GroundBottom

B0, B1, B2, B3, B4, B5, B6, B7, B8, B9
D0, D1, D2, D3, D4, D6, D7, U0, U1, U2, U4, U5

Target
TargetBorderLeft
TargetBorderRight
TargetBorderTop
TargetBorderBottom

Water
WaterSprite
Bridge
BridgeCorner:up:left:color
BridgeCorner:down:left:color
BridgeCorner:down:right:color
BridgeCorner:up:right:color
SinkingPlayerSprite1, SinkingPlayerSprite2, SinkingPlayerSprite3


LightBridge:horizontal
LightBridge:vertical

LightBridgeEnteringPortal:portal_color
LightBridgeSprite:smod3:horizontal
LightBridgeSprite:smod3:vertical

LightBridgeStarter:directions


BlockPlayer, Wall, DarkWall , PortalGunTM
DarkWallSprite
BlockCorner:up:left:color
BlockCorner:down:left:color
BlockCorner:down:right:color
BlockCorner:up:right:color

HintTerminal1, HintTerminal2

WallSeparatorD
WallSeparatorU
WallSeparatorL
WallSeparatorR


DarkWallTop
DarkWallLeft
DarkWallRight
DarkWallBottom
WallOdd
WallTop:white
WallLeft:white
WallRight:white
WallBottom:white
Wallx1:white
WallDarkCorner:white



ParticleSprite1:portal_color:directions ParticleSprite2:portal_color:directions

WantL
WantR
WantD
WantU
Error
PostError
MightSink
OriginL
OriginR
OriginD
OriginU
Portal:blue
Portal1Sprite
Portal:orange
Portal2Sprite
PortalExit:blue
PortalExit:orange
ActivePortal:right
ActivePortal:left
ActivePortal:down
ActivePortal:up
Rotate
OkRigid:left
OkRigid:right
OkRigid:down
OkRigid:up
RigidEnteringPortal1
RigidLeavingPortal1
RigidEnteringPortal2
RigidLeavingPortal2
BPEnteringPortal1, BPLeavingPortal1
BPEnteringPortal2, BPLeavingPortal2
NoPortal1
NoPortal2
Shot:portal_color:directions, ShotF:portal_color:directions
ColorToggle
MouseButton:horizontal
GotGun

HasCellU
HasCellD
HasCellR
HasCellL
RealTimeTick
PlayerNotFound

ThroughPortal:up
ThroughPortal:right
ThroughPortal:left
ThroughPortal:down

FutureUsed
RigidNextToPortal
UnmovablePortal

ErrorSprite
ConveyorLeft

JustTurned
Pointer
PointerEnd, EmptySpot
GoalSoundSprite
CellOdd, CellEven
MissingPlayer

======
RULES
======

down [ b0 | b0 ] -> [ b0 u0 | b0 d0 ]
down [ b1 | b1 ] -> [ b1 u1 | b1 d1 ]
down [ b2 | b2 ] -> [ b2 u2 | b2 d2 ]
down [ b3 | b3 ] -> [ b3 u2 | b3 d3 ]
down [ b4 | b4 ] -> [ b4 u4 | b4 d4 ]
down [ b5 | b5 ] -> [ b5 u5 | b5 d3 ]
down [ b6 | b6 ] -> [ b6 u5 | b6 d6 ]
down [ b7 | b7 ] -> [ b7 u2 | b7 d7 ]
down [ b8 | b8 ] -> [ b8 u0 | b8 d6 ]
down [ b9 | b9 ] -> [ b9 u0 | b9 d3 ]
down [ b6 | b0 ] -> [ b6 u5 | b0 d0 ]


startloop
  (Remove U)
  down  [ no With:down | BlockStub:_U:dd:ll:rr:color ] -> [ | BlockStub:__:dd:ll:rr:color ]
  
  (Remove D)
  up    [ no With:up   | BlockStub:uu:_D:ll:rr:color ] -> [ | BlockStub:uu:__:ll:rr:color ]
  
  (Remove L)
  right [ no With:right| BlockStub:uu:dd:_L:rr:color ] -> [ | BlockStub:uu:dd:__:rr:color ]
  
  (Remove R)
  left  [ no With:left | BlockStub:uu:dd:ll:_R:color ] -> [ | BlockStub:uu:dd:ll:__:color ]

endloop


[ BlockStub:uu:dd:ll:rr:color ] -> [ Block:uu:dd:ll:rr:color ]


[ moving MissingPlayer ] -> [ action MissingPlayer] Message You are dead. R = Restart. Z+Z = Undo.


(random [ action PlayerN ] -> [ PlayerN ]  win)


( This is mostly so that moving the direction keys doesn't change the state when the player
is dead and thus undo will work in one press )
[ ] -> [ PlayerNotFound ]
[ PlayerNotFound ][ SinkingPlayerSprite1 ] -> [][ SinkingPlayerSprite1 ]
[ PlayerNotFound ][ SinkingPlayerSprite2 ] -> [][ SinkingPlayerSprite2 ]
[ PlayerNotFound ][ SinkingPlayerSprite3 ] -> [][ SinkingPlayerSprite3 ]
[ PlayerNotFound ][ RealPlayer ] -> [][ RealPlayer ]
[ PlayerNotFound ][ action MissingPlayer ] -> [][ action MissingPlayer ]
[ PlayerNotFound  ] -> Cancel

[ action MissingPlayer ] -> [ ]

[ RealTimeTick ] -> []
[ stationary Player ] -> [ Player RealTimeTick ]


( some decorative stuff )
  [ HintTerminal2 DarkWall ] -> [ action DarkWall ]
  [ HintTerminal1          ] -> [ HintTerminal2 ]
  [ action DarkWall        ] -> [ HintTerminal1 DarkWall ]
  

  
  [RealTimeTick][ ParticleSprite2:portal_color:directions ] ->  [RealTimeTick][  ]
  
  [RealTimeTick][ ParticleSprite1:portal_color:directions ] ->  [RealTimeTick][ ParticleSprite2:portal_color:directions ]
  
( / some decorative stuff )

[ JustTurned ] -> []

( Instead of > replace it with a Want: towards the same direction )
[ > Player ] -> [ Want:>  stationary Player ]



( Turn the player if they are not alrady facing that direction)
directions weapon_status [ Want:directions Player:weapon_status     no Player:directions ] -> [ Player:weapon_status:directions     JustTurned ]

(Add Origin to the direction)
directions [ Player:directions Want:directions ] -> [ Player:directions Want:directions Origin:directions ]


(Move player after turning unless there was water or a white object next )
[ Background water no LightBridge ] -> [ water action Background ]
[ Player:> JustTurned | no WithWhite stationary Background ] -> [ Player:> Want:> Origin:> | Background ]
[ action Background ] -> [ Background ]


(..)
[ ConveyorLeft Block ] -> [ OriginL WantL Block ConveyorLeft ]

(If a disarmed player moves towards the portal gun, add the GotGun helper and play fx)
[ Player:disarmed:> Origin Want:> | PortalGunTM ] -> [ | Player:disarmed:front GotGun ] sfx1

(A disarmed player gets a portal gun, thus gains the blue property. Man I love PatternScript )
[ Player:disarmed GotGun ] -> [ Player:blue:front ] checkpoint message After aiming, press 'X' to shoot. You can change the color with a mouse click.

weapon_status [ Player:weapon_status:> | HintTerminal ] -> [ Player:weapon_status:< | HintTerminal ] sfx1 message You can switch portal colors by shooting at a portal of the same color. Or you can use a mouse click


(Cleanup light bridges)
[ LightBridge ] -> [ ]

(Prepare portal helpers)

(The portal exit is the cell a portal points to, it shares direction and color with the portal)
portal_color [ Portal:portal_color:> | No PortalExit:portal_color:> ] -> [ Portal:portal_color:> | PortalExit:portal_color:> ]

(In many rules we are just interested in knowing if there is an active portal facing a direction
at a certain cell, the rest doesn't mater as much, that's what ActivePortal: is for)
directions portal_color [ Portal:portal_color:directions  ][ Portal:toggle_portal_color] -> [ Portal:portal_color:directions   ActivePortal:directions ][ Portal:toggle_portal_color ]

( Do not consider a portal active if it is right next to a wall )
( This fixes situations in which you try to push a rigid body through a portal
 blocked by a wall, the rigid can disarm in that case)
(This part uses the action on wall to signify that active portals must be deleted)
[ ActivePortal:> | DarkOrWhiteWall ] -> [  | action DarkOrWhiteWall ]

[ action DarkOrWhiteWall ][ ActivePortal ] -> [ action DarkOrWhiteWall ][]
[ action DarkOrWhiteWall ] -> [ stationary DarkOrWhiteWall ]




( Detect rigids through portals )
[ ThroughPortal ] -> []

[ With:> | ActivePortal:< ] -> [ With:> ThroughPortal:> | ActivePortal:< ]


(For animation purposes, turn ShotF into Shot, this happens after 'again')
portal_color directions [ ShotF:portal_color:directions ] -> [ Shot:portal_color:directions ]


( Mouse Click Events )
[ MouseButton:horizontal ] -> [ ColorToggle ]


( Cancel shot if there are with:directions's through portals) 
[ ThroughPortal ][ action Player ] -> [ ThroughPortal ][ Player ] sfx9

( Shoot a portal )
portal_color directions [ action Player:portal_color:directions ] -> [ Player:portal_color:directions Shot:portal_color:directions ] sfx2

(Do not allow RealTime stuff when there's a shot visible)
[ BlueShot ][ RealTimeTick ] -> [ BlueShot ][ ]
[ OrangeShot ][ RealTimeTick ] -> [ OrangeShot ][ ]
([ BlueShot ][ Portal:blue ] -> [ BlueShot ][ ]
[ OrangeShot ][ Portal:orange ] -> [ OrangeShot ][ ])

[ BlueShot   ][ Portal1SPrite ] -> [ BlueShot ][ ]
[ OrangeShot ][ Portal2SPrite ] -> [ OrangeShot ][ ]

( Move the portal shot, but use the F sprite and schedule an again)
( It's a bit counter intuitive but we have to use > syntax here instead of the directions mapping )
portal_color [ Shot:portal_color:> | no WithCollision ] -> [ | ShotF:portal_color:> ] again


(Delete the old Portal if the shot is about to create a new one)
portal_color [Portal:portal_color][ Shot:portal_color:> | WithWhite ] -> [][ Shot:portal_color:>| WithWhite ]

(Create the portal)
portal_color [ Shot:portal_color:> | WithWhite ] -> [ ColorToggle | Portal:portal_color:< WithWhite ] sfx3


( Prevent overlapping portals )
directions portal_color [ Portal:blue:directions  Portal:orange:directions  ][ Player:portal_color   ] -> [ Portal:portal_color:directions   ][ Player:portal_color   ]

(Draw portal particle sprite when it hits something that can't open a portal)
portal_color [ Shot:portal_color:>    | WithCollision ] -> [ | ParticleSprite1:portal_color:< WithCollision ]

(Toggle the portal gun's color)
portal_color player_face [ ColorToggle ][ Player:portal_color:player_face ] -> [ ][ Player:toggle_portal_color:player_face ]

(This logic is to make portals unmovable if there's a rigid going through them)


[ UnmovablePortal ] -> []
[ RigidNextToPortal ] -> []
portal_color [ Portal:portal_color:> ActivePortal:> | With:< ] -> [ Portal:portal_color:> ActivePortal:> UnmovablePortal | With:< RigidNexttoPortal:< ]


(===========================================================================================
BEGINNING OF MOVEMENT LOGIC

Before rewrite, it generated 14437 instructions
)


(  spread  )
startloop



  left  [ BlockPlayer WantL No ActivePortal:left  | Block No WantL No ActivePortal:right No UnmovablePortal ] -> [ BlockPlayer WantL | Block WantL ]
  right [ BlockPlayer WantR No ActivePortal:right | Block No WantR No ActivePortal:left  No UnmovablePortal ] -> [ BlockPlayer WantR | Block WantR ]
  down  [ BlockPlayer WantD No ActivePortal:down  | Block No WantD No ActivePortal:up    No UnmovablePortal ] -> [ BlockPlayer WantD | Block WantD ]
  up    [ BlockPlayer WantU No ActivePortal:up    | Block No WantU No ActivePortal:down  No UnmovablePortal ] -> [ BlockPlayer WantU | Block WantU ]


  left  [ WantL With:left | With:right no WantL ] -> [ WantL With:left | With:right WantL ]
  right [ WantL With:right | With:left no WantL ] -> [ WantL With:right | With:left WantL ]
  up    [ WantL With:up | With:down no WantL ] -> [ WantL With:up | With:down WantL ]
  down  [ WantL With:down | With:up no WantL ] -> [ WantL With:down | With:up WantL ]

  left  [ WantR With:left | With:right no WantR ] -> [ WantR With:left | With:right WantR ]
  right [ WantR With:right | With:left no WantR ] -> [ WantR With:right | With:left WantR ]
  up    [ WantR With:up | With:down no WantR ] -> [ WantR With:up | With:down WantR ]
  down  [ WantR With:down | With:up no WantR ] -> [ WantR With:down | With:up WantR ]

  left  [ WantU With:left | With:right no WantU ] -> [ WantU With:left | With:right WantU ]
  right [ WantU With:right | With:left no WantU ] -> [ WantU With:right | With:left WantU ]
  up    [ WantU With:up | With:down no WantU ] -> [ WantU With:up | With:down WantU ]
  down  [ WantU With:down | With:up no WantU ] -> [ WantU With:down | With:up WantU ]

  left  [ WantD With:left | With:right no WantD ] -> [ WantD With:left | With:right WantD ]
  right [ WantD With:right | With:left no WantD ] -> [ WantD With:right | With:left WantD ]
  up    [ WantD With:up | With:down no WantD ] -> [ WantD With:up | With:down WantD ]
  down  [ WantD With:down | With:up no WantD ] -> [ WantD With:down | With:up WantD ]



  ( Spread rigidity through portals )
  right [ Portal:blue:right| WantR With:left no RigidLeavingPortal1 ] -> [ Portal:blue:right UnmovablePortal | WantR With:left RigidLeavingPortal1 ]
  left  [ Portal:blue:left| WantL With:right no RigidLeavingPortal1 ] -> [ Portal:blue:left UnmovablePortal | WantL With:right RigidLeavingPortal1 ]
  up    [ Portal:blue:up| WantU With:down no RigidLeavingPortal1 ] -> [ Portal:blue:up UnmovablePortal | WantU With:down RigidLeavingPortal1 ]
  down  [ Portal:blue:down| WantD With:up no RigidLeavingPortal1 ] -> [ Portal:blue:down UnmovablePortal | WantD With:up RigidLeavingPortal1 ]
  right [ Portal:orange:right| WantR With:left no RigidLeavingPortal2 ] -> [ Portal:orange:right UnmovablePortal | WantR With:left RigidLeavingPortal2 ]
  left  [ Portal:orange:left| WantL With:right no RigidLeavingPortal2 ] -> [ Portal:orange:left UnmovablePortal | WantL With:right RigidLeavingPortal2 ]
  up    [ Portal:orange:up| WantU With:down no RigidLeavingPortal2 ] -> [ Portal:orange:up UnmovablePortal | WantU With:down RigidLeavingPortal2 ]
  down  [ Portal:orange:down| WantD With:up no RigidLeavingPortal2 ] -> [ Portal:orange:down UnmovablePortal | WantD With:up RigidLeavingPortal2 ]
  
  right [ Portal:blue:right| WantL With:left no RigidEnteringPortal1] -> [ Portal:blue:right UnmovablePortal | WantL With:left RigidEnteringPortal1]
  left  [ Portal:blue:left| WantR With:right no RigidEnteringPortal1] -> [ Portal:blue:left UnmovablePortal | WantR With:right RigidEnteringPortal1]
  up    [ Portal:blue:up| WantD With:down no RigidEnteringPortal1] -> [ Portal:blue:up UnmovablePortal | WantD With:down RigidEnteringPortal1]
  down  [ Portal:blue:down| WantU With:up no RigidEnteringPortal1] -> [ Portal:blue:down UnmovablePortal | WantU With:up RigidEnteringPortal1]
  right [ Portal:orange:right| WantL With:left no RigidEnteringPortal2] -> [ Portal:orange:right UnmovablePortal | WantL With:left RigidEnteringPortal2]
  left  [ Portal:orange:left| WantR With:right no RigidEnteringPortal2] -> [ Portal:orange:left UnmovablePortal | WantR With:right RigidEnteringPortal2]
  up    [ Portal:orange:up| WantD With:down no RigidEnteringPortal2] -> [ Portal:orange:up UnmovablePortal | WantD With:down RigidEnteringPortal2]
  down  [ Portal:orange:down| WantU With:up no RigidEnteringPortal2] -> [ Portal:orange:down UnmovablePortal | WantU With:up RigidEnteringPortal2]  
  
  right [ Portal:blue:right| With:left no WantR ][ RigidEnteringPortal2 ] -> [ Portal:blue:right UnmovablePortal | With:left WantR ][ RigidEnteringPortal2 ]
  left  [ Portal:blue:left| With:right no WantL ][ RigidEnteringPortal2 ] -> [ Portal:blue:left UnmovablePortal | With:right WantL ][ RigidEnteringPortal2 ]
  up    [ Portal:blue:up| With:down no WantU ][ RigidEnteringPortal2 ] -> [ Portal:blue:up UnmovablePortal | With:down WantU ][ RigidEnteringPortal2 ]
  down  [ Portal:blue:down| With:up no WantD ][ RigidEnteringPortal2 ] -> [ Portal:blue:down UnmovablePortal | With:up WantD ][ RigidEnteringPortal2 ]
  right [ Portal:orange:right| With:left no WantR ][ RigidEnteringPortal1 ] -> [ Portal:orange:right UnmovablePortal | With:left WantR ][ RigidEnteringPortal1 ]
  left  [ Portal:orange:left| With:right no WantL ][ RigidEnteringPortal1 ] -> [ Portal:orange:left UnmovablePortal | With:right WantL ][ RigidEnteringPortal1 ]
  up    [ Portal:orange:up| With:down no WantU ][ RigidEnteringPortal1 ] -> [ Portal:orange:up UnmovablePortal | With:down WantU ][ RigidEnteringPortal1 ]
  down  [ Portal:orange:down| With:up no WantD ][ RigidEnteringPortal1 ] -> [ Portal:orange:down UnmovablePortal | With:up WantD ][ RigidEnteringPortal1 ]

  right [ Portal:blue:right| With:left no WantL ][ RigidLeavingPortal2  ] -> [ Portal:blue:right UnmovablePortal | With:left WantL ][ RigidLeavingPortal2  ]
  left  [ Portal:blue:left| With:right no WantR ][ RigidLeavingPortal2  ] -> [ Portal:blue:left UnmovablePortal | With:right WantR ][ RigidLeavingPortal2  ]
  up    [ Portal:blue:up| With:down no WantD ][ RigidLeavingPortal2  ] -> [ Portal:blue:up UnmovablePortal | With:down WantD ][ RigidLeavingPortal2  ]
  down  [ Portal:blue:down| With:up no WantU ][ RigidLeavingPortal2  ] -> [ Portal:blue:down UnmovablePortal | With:up WantU ][ RigidLeavingPortal2  ]
  right [ Portal:orange:right| With:left no WantL ][ RigidLeavingPortal1  ] -> [ Portal:orange:right UnmovablePortal | With:left WantL ][ RigidLeavingPortal1  ]
  left  [ Portal:orange:left| With:right no WantR ][ RigidLeavingPortal1  ] -> [ Portal:orange:left UnmovablePortal | With:right WantR ][ RigidLeavingPortal1  ]
  up    [ Portal:orange:up| With:down no WantD ][ RigidLeavingPortal1  ] -> [ Portal:orange:up UnmovablePortal | With:down WantD ][ RigidLeavingPortal1  ]
  down  [ Portal:orange:down| With:up no WantU ][ RigidLeavingPortal1  ] -> [ Portal:orange:down UnmovablePortal | With:up WantU ][ RigidLeavingPortal1  ]
  
  ( BlockPlayer pushing through portal )
  right [ Portal:blue:right ActivePortal:right | BlockPlayer WantL no BPEnteringPortal1 no With:up no With:down ] -> [ Portal:blue:right ActivePortal:right UnmovablePortal | BlockPlayer WantL BPEnteringPortal1 ]
  left  [ Portal:blue:left ActivePortal:left  | BlockPlayer WantR no BPEnteringPortal1 no With:up no With:down ] -> [ Portal:blue:left ActivePortal:left  UnmovablePortal | BlockPlayer WantR BPEnteringPortal1 ]
  up    [ Portal:blue:up ActivePortal:up    | BlockPlayer WantD no BPEnteringPortal1 no With:right no With:left ] -> [ Portal:blue:up ActivePortal:up    UnmovablePortal | BlockPlayer WantD BPEnteringPortal1 ]
  down  [ Portal:blue:down ActivePortal:down  | BlockPlayer WantU no BPEnteringPortal1 no With:right no With:left ] -> [ Portal:blue:down ActivePortal:down  UnmovablePortal | BlockPlayer WantU BPEnteringPortal1 ]
  right [ Portal:orange:right ActivePortal:right | BlockPlayer WantL no BPEnteringPortal2 no With:up no With:down ] -> [ Portal:orange:right ActivePortal:right UnmovablePortal | BlockPlayer WantL BPEnteringPortal2 ]
  left  [ Portal:orange:left ActivePortal:left  | BlockPlayer WantR no BPEnteringPortal2 no With:up no With:down ] -> [ Portal:orange:left ActivePortal:left  UnmovablePortal | BlockPlayer WantR BPEnteringPortal2 ]
  up    [ Portal:orange:up ActivePortal:up    | BlockPlayer WantD no BPEnteringPortal2 no With:right no With:left ] -> [ Portal:orange:up ActivePortal:up    UnmovablePortal | BlockPlayer WantD BPEnteringPortal2 ]
  down  [ Portal:orange:down ActivePortal:down  | BlockPlayer WantU no BPEnteringPortal2 no With:right no With:left ] -> [ Portal:orange:down ActivePortal:down  UnmovablePortal | BlockPlayer WantU BPEnteringPortal2 ]
  
  right [ Portal:blue:right| BlockPlayer No WantR ][ BPEnteringPortal2 ] -> [ Portal:blue:right UnmovablePortal | BlockPlayer WantR ][ BPEnteringPortal2 ]
  left  [ Portal:blue:left| BlockPlayer No WantL ][ BPEnteringPortal2 ] -> [ Portal:blue:left UnmovablePortal | BlockPlayer WantL ][ BPEnteringPortal2 ]
  up    [ Portal:blue:up| BlockPlayer No WantU ][ BPEnteringPortal2 ] -> [ Portal:blue:up UnmovablePortal | BlockPlayer WantU ][ BPEnteringPortal2 ]
  down  [ Portal:blue:down| BlockPlayer No WantD ][ BPEnteringPortal2 ] -> [ Portal:blue:down UnmovablePortal | BlockPlayer WantD ][ BPEnteringPortal2 ]
  right [ Portal:orange:right| BlockPlayer No WantR ][ BPEnteringPortal1 ] -> [ Portal:orange:right UnmovablePortal | BlockPlayer WantR ][ BPEnteringPortal1 ]
  left  [ Portal:orange:left| BlockPlayer No WantL ][ BPEnteringPortal1 ] -> [ Portal:orange:left UnmovablePortal | BlockPlayer WantL ][ BPEnteringPortal1 ]
  up    [ Portal:orange:up| BlockPlayer No WantU ][ BPEnteringPortal1 ] -> [ Portal:orange:up UnmovablePortal | BlockPlayer WantU ][ BPEnteringPortal1 ]
  down  [ Portal:orange:down| BlockPlayer No WantD ][ BPEnteringPortal1 ] -> [ Portal:orange:down UnmovablePortal | BlockPlayer WantD ][ BPEnteringPortal1 ]

  [ Portal:blue    UnmovablePortal ][ Portal:orange No UnmovablePortal ] -> [ Portal:blue UnmovablePortal ][ Portal:orange UnmovablePortal ]
  [ Portal:blue No UnmovablePortal ][ Portal:orange    UnmovablePortal ] -> [ Portal:blue UnmovablePortal ][ Portal:orange UnmovablePortal ]

endloop

(  detect error )



[ FutureUsed ] -> []
( detect objects moving to the same place )
right [ WantR no BPEnteringPortal1 no BPEnteringPortal2 | FutureUsed ] -> [ WantR | FutureUsed Error ]
right [ WantR no BPEnteringPortal1 no BPEnteringPortal2 |            ] -> [       WantR | FutureUsed ]

left  [ WantL no BPEnteringPortal1 no BPEnteringPortal2 | FutureUsed ] -> [ WantL | FutureUsed Error ]
left  [ WantL no BPEnteringPortal1 no BPEnteringPortal2 |            ] -> [       WantL | FutureUsed ]

up    [ WantU no BPEnteringPortal1 no BPEnteringPortal2 | FutureUsed ] -> [ WantU | FutureUsed Error ]
up    [ WantU no BPEnteringPortal1 no BPEnteringPortal2 |            ] -> [       WantU | FutureUsed ]

down  [ WantD no BPEnteringPortal1 no BPEnteringPortal2 | FutureUsed ] -> [  WantD | FutureUsed Error ]
down  [ WantD no BPEnteringPortal1 no BPEnteringPortal2 |            ] -> [       WantD | FutureUsed ]


right [ WantR BPEnteringPortal1 ][ PortalExit:orange FutureUsed ] -> [ Error WantR BPEnteringPortal1 ][ PortalExit:orange FutureUsed ]
right [ WantR BPEnteringPortal1 ][ PortalExit:orange            ] -> [       WantR BPEnteringPortal1 ][ PortalExit:orange FutureUsed ]
right [ WantR BPEnteringPortal2 ][ PortalExit:blue   FutureUsed ] -> [ Error WantR BPEnteringPortal2 ][ PortalExit:blue   FutureUsed ]
right [ WantR BPEnteringPortal2 ][ PortalExit:blue              ] -> [       WantR BPEnteringPortal2 ][ PortalExit:blue   FutureUsed ]

left  [ WantL BPEnteringPortal1 ][ PortalExit:orange FutureUsed ] -> [ Error WantL BPEnteringPortal1 ][ PortalExit:orange FutureUsed ]
left  [ WantL BPEnteringPortal1 ][ PortalExit:orange            ] -> [       WantL BPEnteringPortal1 ][ PortalExit:orange FutureUsed ]
left  [ WantL BPEnteringPortal2 ][ PortalExit:blue   FutureUsed ] -> [ Error WantL BPEnteringPortal2 ][ PortalExit:blue   FutureUsed ]
left  [ WantL BPEnteringPortal2 ][ PortalExit:blue              ] -> [       WantL BPEnteringPortal2 ][ PortalExit:blue   FutureUsed ]

up    [ WantU BPEnteringPortal1 ][ PortalExit:orange FutureUsed ] -> [ Error WantU BPEnteringPortal1 ][ PortalExit:orange FutureUsed ]
up    [ WantU BPEnteringPortal1 ][ PortalExit:orange            ] -> [       WantU BPEnteringPortal1 ][ PortalExit:orange FutureUsed ]
up    [ WantU BPEnteringPortal2 ][ PortalExit:blue   FutureUsed ] -> [ Error WantU BPEnteringPortal2 ][ PortalExit:blue   FutureUsed ]
up    [ WantU BPEnteringPortal2 ][ PortalExit:blue              ] -> [       WantU BPEnteringPortal2 ][ PortalExit:blue   FutureUsed ]

down  [ WantD BPEnteringPortal1 ][ PortalExit:orange FutureUsed ] -> [ Error WantD BPEnteringPortal1 ][ PortalExit:orange FutureUsed ]
down  [ WantD BPEnteringPortal1 ][ PortalExit:orange            ] -> [       WantD BPEnteringPortal1 ][ PortalExit:orange FutureUsed ]
down  [ WantD BPEnteringPortal2 ][ PortalExit:blue   FutureUsed ] -> [ Error WantD BPEnteringPortal2 ][ PortalExit:blue   FutureUsed ]
down  [ WantD BPEnteringPortal2 ][ PortalExit:blue              ] -> [       WantD BPEnteringPortal2 ][ PortalExit:blue   FutureUsed ]

( /detect objects moving to the same place )



startloop
  [ BPEnteringPortal1 ][ Portal:blue ] -> [ BPEnteringPortal1 ][ UnmovablePortal Portal:blue ]
  [ BPEnteringPortal2 ][ Portal:orange ] -> [ BPEnteringPortal2 ][ UnmovablePortal Portal:orange ]

  (   hit something then add error)
  left  [ WantL No Error | WithCollision no WantL no ActivePortal:right ] -> [ WantL Error PostError | WithCollision ]
  right [ WantR No Error | WithCollision no WantR no ActivePortal:left  ] -> [ WantR Error PostError | WithCollision ]
  down  [ WantD No Error | WithCollision no WantD no ActivePortal:up    ] -> [ WantD Error PostError | WithCollision ]
  up    [ WantU No Error | WithCollision no WantU no ActivePortal:down  ] -> [ WantU Error PostError | WithCollision ]


  left  [ WantL No Error UnmovablePortal ] -> [ WantL Error PostError UnmovablePortal ]
  right [ WantR No Error UnmovablePortal ] -> [ WantR Error PostError UnmovablePortal ]
  down  [ WantD No Error UnmovablePortal ] -> [ WantD Error PostError UnmovablePortal ]
  up    [ WantU No Error UnmovablePortal ] -> [ WantU Error PostError UnmovablePortal ]


  ( Attempt to push rigid through portal )

  (left [ wantL No Error WithVertical   | ActivePortal:right ] -> [ wantL Error WithVertical   | ActivePortal:right ]
  right[ wantR No Error WithVertical   | ActivePortal:left  ] -> [ wantR Error WithVertical   | ActivePortal:left ]
  down [ wantD No Error WithHorizontal | ActivePortal:up    ] -> [ wantD Error WithHorizontal | ActivePortal:up ]
  up   [ wantU No Error WithHorizontal | ActivePortal:down  ] -> [ wantU Error WithHorizontal | ActivePortal:down ])

  ( Cant cut )
  [ Want:vertical   RigidNextToPortal:horizontal ] -> [ Want:vertical RigidNextToPortal:horizontal Error ]
  [ Want:horizontal RigidNextToPortal:vertical   ] -> [ Want:horizontal  RigidNextToPortal:vertical Error ]
  
  right [ ActivePortal:right | WantL WithVertical ] -> [ ActivePortal:right | WantL WithVertical Error ] 
  left  [ ActivePortal:left  | WantR WithVertical ] -> [ ActivePortal:left  | WantR WithVertical Error ] 
  up    [ ActivePortal:up    | WantD WithHorizontal]-> [ ActivePortal:up    | WantD WithHorizontal Error ] 
  down  [ ActivePortal:down  | WantU WithHorizontal]-> [ ActivePortal:down  | WantU WithHorizontal Error ] 




  ( Cant push and cut )
  [ Want Portal:blue No Error ][ RigidNextToPortal ] -> [ Want Portal:blue Error ][ RigidNextToPortal ] 
  [ Want Portal:orange No Error ][ RigidNextToPortal ] -> [ Want Portal:orange Error ][ RigidNextToPortal ] 
  
  ( Contradiction, then add error )
  [ WantL WantNotL ] -> [ WantL WantNotL Error ]
  [ WantR WantNotR ] -> [ WantR WantNotR Error ]
  [ WantD WantNotD ] -> [ WantD WantNotD Error ]
  [ WantU WantNotU ] -> [ WantU WantNotU Error ]

  (   hit error , then add error)
  left [ WantL No Error | WithCollision Error ] -> [ WantL Error PostError | WithCollision Error ]
  right [ WantR No Error | WithCollision Error ] -> [ WantR Error PostError | WithCollision Error ]
  down [ WantD No Error | WithCollision Error ] -> [ WantD Error PostError | WithCollision Error ]
  up [ WantU No Error | WithCollision Error ] -> [ WantU Error PostError | WithCollision Error ]


  (   player attempting to push error, then add error)
  left [ BlockPlayer WantL no Error | Block WantL Error ] -> [  BlockPlayer WantL Error | Block WantL Error ]
  right [  BlockPlayer WantR no Error | Block WantR Error ] -> [  BlockPlayer WantR Error | Block WantR Error ]
  down [  BlockPlayer WantD no Error | Block WantD Error ] -> [  BlockPlayer WantD Error | Block WantD Error ]
  up [  BlockPlayer WantU no Error | Block WantU Error ] -> [  BlockPlayer WantU Error | Block WantU Error ]


  (   contiguous to error, then add error)
  left  [ Want With:left Error | With:right Want no Error ] -> [ Want With:left Error | With:right Want Error ]
  right  [ Want With:right Error | With:left Want no Error ] -> [ Want With:right Error | With:left Want Error ]
  down  [ Want With:down Error | With:up Want no Error ] -> [ Want With:down Error | With:up Want Error ]
  up  [ Want With:up Error | With:down Want no Error ] -> [ Want With:up Error | With:down Want Error ]

  ( Portal attempting to enter another portal )
  right [ Portal:blue wantR | Portal:orange:left] -> [ Portal:blue wantR Error | Portal:orange:left]
  left  [ Portal:blue wantL | Portal:orange:right] -> [ Portal:blue wantL Error | Portal:orange:right]
  up    [ Portal:blue wantU | Portal:orange:down] -> [ Portal:blue wantU Error | Portal:orange:down]
  down  [ Portal:blue wantD | Portal:orange:up] -> [ Portal:blue wantD Error | Portal:orange:up]
  right [ Portal:orange wantR | Portal:blue:left] -> [ Portal:orange wantR Error | Portal:blue:left]
  left  [ Portal:orange wantL | Portal:blue:right] -> [ Portal:orange wantL Error | Portal:blue:right]
  up    [ Portal:orange wantU | Portal:blue:down] -> [ Portal:orange wantU Error | Portal:blue:down]
  down  [ Portal:orange wantD | Portal:blue:up] -> [ Portal:orange wantD Error | Portal:blue:up]


  ( > spread errors through portals )
  right [ Portal:blue:right| BlockPlayer WantR no BPLeavingPortal1 ] -> [ Portal:blue:right| BlockPlayer WantR BPLeavingPortal1 ]
  left  [ Portal:blue:left| BlockPlayer WantL no BPLeavingPortal1 ] -> [ Portal:blue:left| BlockPlayer WantL BPLeavingPortal1 ]
  up    [ Portal:blue:up| BlockPlayer WantU no BPLeavingPortal1 ] -> [ Portal:blue:up| BlockPlayer WantU BPLeavingPortal1 ]
  down  [ Portal:blue:down| BlockPlayer WantD no BPLeavingPortal1 ] -> [ Portal:blue:down| BlockPlayer WantD BPLeavingPortal1 ]
  right [ Portal:orange:right| BlockPlayer WantR no BPLeavingPortal2 ] -> [ Portal:orange:right| BlockPlayer WantR BPLeavingPortal2 ]
  left  [ Portal:orange:left| BlockPlayer WantL no BPLeavingPortal2 ] -> [ Portal:orange:left| BlockPlayer WantL BPLeavingPortal2 ]
  up    [ Portal:orange:up| BlockPlayer WantU no BPLeavingPortal2 ] -> [ Portal:orange:up| BlockPlayer WantU BPLeavingPortal2 ]
  down  [ Portal:orange:down| BlockPlayer WantD no BPLeavingPortal2 ] -> [ Portal:orange:down| BlockPlayer WantD BPLeavingPortal2 ]
  
  [ BPEnteringPortal1 no Error ][ BPLeavingPortal2 Error ] -> [ BPEnteringPortal1 Error ][ BPLeavingPortal2 Error ]
  [ BPEnteringPortal2 no Error ][ BPLeavingPortal1 Error ] -> [ BPEnteringPortal2 Error ][ BPLeavingPortal1 Error ]
  

endloop




( Remove wants)
[ Want ] -> [  ]

( Spread Wants again , but this time can't spread through errors)

[ OriginL no Error ] -> [WantL]
[ OriginR no Error ] -> [WantR]
[ OriginD no Error ] -> [WantD]
[ OriginU no Error ] -> [WantU]


[ Origin ] -> []

[ RigidEnteringPortal1 ] -> []
[ RigidEnteringPortal2 ] -> []
[ RigidLeavingPortal1 ] -> []
[ RigidLeavingPortal2 ] -> []
[ BPEnteringPortal1 ] -> []
[ BPEnteringPortal2 ] -> []
[ BPLeavingPortal1 ] -> []
[ BPLeavingPortal2 ] -> []



(  spread again, blocked by errors )


startloop
  left  [ BlockPlayer WantL No BPEnteringPortal1 No BPEnteringPortal2 | Block No WantL No Error No UnmovablePortal No ActivePortal:right ] -> [ BlockPlayer WantL | Block WantL ]
  right [ BlockPlayer WantR No BPEnteringPortal1 No BPEnteringPortal2 | Block No WantR No Error No UnmovablePortal No ActivePortal:left ] -> [ BlockPlayer WantR | Block WantR ]
  down  [ BlockPlayer WantD No BPEnteringPortal1 No BPEnteringPortal2 | Block No WantD no Error No UnmovablePortal No ActivePortal:up ] -> [ BlockPlayer WantD | Block WantD ]
  up    [ BlockPlayer WantU No BPEnteringPortal1 No BPEnteringPortal2 | Block No WantU no Error No UnmovablePortal No ActivePortal:down ] -> [ BlockPlayer WantU | Block WantU ]



  left  [ Want With:left | With:right no Want no Error ] -> [ Want With:left | With:right Want ]
  right  [ Want With:right | With:left no Want no Error ] -> [ Want With:right | With:left Want ]
  up  [ Want With:up | With:down no Want no Error ] -> [ Want With:up | With:down Want ]
  down  [ Want With:down | With:up no Want no Error ] -> [ Want With:down | With:up Want ]
 
  ( Spread rigidity through portals minus error )
  right [ Portal:blue:right| WantR With:left no RigidLeavingPortal1  ] -> [ Portal:blue:right| WantR With:left RigidLeavingPortal1 ]
  left  [ Portal:blue:left| WantL With:right no RigidLeavingPortal1 ] -> [ Portal:blue:left| WantL With:right RigidLeavingPortal1 ]
  up    [ Portal:blue:up| WantU With:down no RigidLeavingPortal1 ] -> [ Portal:blue:up| WantU With:down RigidLeavingPortal1 ]
  down  [ Portal:blue:down| WantD With:up no RigidLeavingPortal1 ] -> [ Portal:blue:down| WantD With:up RigidLeavingPortal1 ]
  right [ Portal:orange:right| WantR With:left no RigidLeavingPortal2   ] -> [ Portal:orange:right| WantR With:left RigidLeavingPortal2 ]
  left  [ Portal:orange:left| WantL With:right no RigidLeavingPortal2   ] -> [ Portal:orange:left| WantL With:right RigidLeavingPortal2 ]
  up    [ Portal:orange:up| WantU With:down no RigidLeavingPortal2 ] -> [ Portal:orange:up| WantU With:down RigidLeavingPortal2 ]
  down  [ Portal:orange:down| WantD With:up no RigidLeavingPortal2 ] -> [ Portal:orange:down| WantD With:up RigidLeavingPortal2 ]
  
  right [ Portal:blue:right| WantL With:left no RigidEnteringPortal1 no WithVertical  ] -> [ Portal:blue:right| WantL With:left RigidEnteringPortal1]
  left  [ Portal:blue:left| WantR With:right no RigidEnteringPortal1 no WithVertical  ] -> [ Portal:blue:left| WantR With:right RigidEnteringPortal1]
  up    [ Portal:blue:up| WantD With:down no RigidEnteringPortal1 no WithHorizontal] -> [ Portal:blue:up| WantD With:down RigidEnteringPortal1]
  down  [ Portal:blue:down| WantU With:up no RigidEnteringPortal1 no WithHorizontal] -> [ Portal:blue:down| WantU With:up RigidEnteringPortal1]
  right [ Portal:orange:right| WantL With:left no RigidEnteringPortal2 no WithVertical  ] -> [ Portal:orange:right| WantL With:left RigidEnteringPortal2]
  left  [ Portal:orange:left| WantR With:right no RigidEnteringPortal2 no WithVertical  ] -> [ Portal:orange:left| WantR With:right RigidEnteringPortal2]
  up    [ Portal:orange:up| WantD With:down no RigidEnteringPortal2 no WithHorizontal] -> [ Portal:orange:up| WantD With:down RigidEnteringPortal2]
  down  [ Portal:orange:down| WantU With:up no RigidEnteringPortal2 no WithHorizontal] -> [ Portal:orange:down| WantU With:up RigidEnteringPortal2]  
  
  right [ Portal:blue:right| With:left no WantR no Error ][ RigidEnteringPortal2 ] -> [ Portal:blue:right| With:left WantR ][ RigidEnteringPortal2 ]
  left  [ Portal:blue:left| With:right no WantL no Error ][ RigidEnteringPortal2 ] -> [ Portal:blue:left| With:right WantL ][ RigidEnteringPortal2 ]
  up    [ Portal:blue:up| With:down no WantU no Error ][ RigidEnteringPortal2 ] -> [ Portal:blue:up| With:down WantU ][ RigidEnteringPortal2 ]
  down  [ Portal:blue:down| With:up no WantD no Error ][ RigidEnteringPortal2 ] -> [ Portal:blue:down| With:up WantD ][ RigidEnteringPortal2 ]
  right [ Portal:orange:right| With:left no WantR no Error ][ RigidEnteringPortal1 ] -> [ Portal:orange:right| With:left WantR ][ RigidEnteringPortal1 ]
  left  [ Portal:orange:left| With:right no WantL no Error ][ RigidEnteringPortal1 ] -> [ Portal:orange:left| With:right WantL ][ RigidEnteringPortal1 ]
  up    [ Portal:orange:up| With:down no WantU no Error ][ RigidEnteringPortal1 ] -> [ Portal:orange:up| With:down WantU ][ RigidEnteringPortal1 ]
  down  [ Portal:orange:down| With:up no WantD no Error ][ RigidEnteringPortal1 ] -> [ Portal:orange:down| With:up WantD ][ RigidEnteringPortal1 ]

  right [ Portal:blue:right| With:left no WantL no Error ][ RigidLeavingPortal2  ] -> [ Portal:blue:right| With:left WantL ][ RigidLeavingPortal2  ]
  left  [ Portal:blue:left| With:right no WantR no Error ][ RigidLeavingPortal2  ] -> [ Portal:blue:left| With:right WantR ][ RigidLeavingPortal2  ]
  up    [ Portal:blue:up| With:down no WantD no Error ][ RigidLeavingPortal2  ] -> [ Portal:blue:up| With:down WantD ][ RigidLeavingPortal2  ]
  down  [ Portal:blue:down| With:up no WantU no Error ][ RigidLeavingPortal2  ] -> [ Portal:blue:down| With:up WantU ][ RigidLeavingPortal2  ]
  right [ Portal:orange:right| With:left no WantL no Error ][ RigidLeavingPortal1  ] -> [ Portal:orange:right| With:left WantL ][ RigidLeavingPortal1  ]
  left  [ Portal:orange:left| With:right no WantR no Error ][ RigidLeavingPortal1  ] -> [ Portal:orange:left| With:right WantR ][ RigidLeavingPortal1  ]
  up    [ Portal:orange:up| With:down no WantD no Error ][ RigidLeavingPortal1  ] -> [ Portal:orange:up| With:down WantD ][ RigidLeavingPortal1  ]
  down  [ Portal:orange:down| With:up no WantU no Error ][ RigidLeavingPortal1  ] -> [ Portal:orange:down| With:up WantU ][ RigidLeavingPortal1  ]
  
  ( BlockPlayer pushing through portal, no error )
  right [ Portal:blue:right| BlockPlayer WantL no BPEnteringPortal1 no WithVertical   ] -> [ Portal:blue:right| BlockPlayer WantL BPEnteringPortal1 ]
  left  [ Portal:blue:left| BlockPlayer WantR no BPEnteringPortal1 no WithVertical   ] -> [ Portal:blue:left| BlockPlayer WantR BPEnteringPortal1 ]
  up    [ Portal:blue:up| BlockPlayer WantD no BPEnteringPortal1 no WithHorizontal ] -> [ Portal:blue:up| BlockPlayer WantD BPEnteringPortal1 ]
  down  [ Portal:blue:down| BlockPlayer WantU no BPEnteringPortal1 no WithHorizontal ] -> [ Portal:blue:down| BlockPlayer WantU BPEnteringPortal1 ]
  right [ Portal:orange:right| BlockPlayer WantL no BPEnteringPortal2 no WithVertical   ] -> [ Portal:orange:right| BlockPlayer WantL BPEnteringPortal2 ]
  left  [ Portal:orange:left| BlockPlayer WantR no BPEnteringPortal2 no WithVertical   ] -> [ Portal:orange:left| BlockPlayer WantR BPEnteringPortal2 ]
  up    [ Portal:orange:up| BlockPlayer WantD no BPEnteringPortal2 no WithHorizontal ] -> [ Portal:orange:up| BlockPlayer WantD BPEnteringPortal2 ]
  down  [ Portal:orange:down| BlockPlayer WantU no BPEnteringPortal2 no WithHorizontal ] -> [ Portal:orange:down| BlockPlayer WantU BPEnteringPortal2 ]
  
  right [ Portal:blue:right| BlockPlayer No WantR no Error ][ BPEnteringPortal2 ] -> [ Portal:blue:right| BlockPlayer WantR ][ BPEnteringPortal2 ]
  left  [ Portal:blue:left| BlockPlayer No WantL no Error ][ BPEnteringPortal2 ] -> [ Portal:blue:left| BlockPlayer WantL ][ BPEnteringPortal2 ]
  up    [ Portal:blue:up| BlockPlayer No WantU no Error ][ BPEnteringPortal2 ] -> [ Portal:blue:up| BlockPlayer WantU ][ BPEnteringPortal2 ]
  down  [ Portal:blue:down| BlockPlayer No WantD no Error ][ BPEnteringPortal2 ] -> [ Portal:blue:down| BlockPlayer WantD ][ BPEnteringPortal2 ]
  right [ Portal:orange:right| BlockPlayer No WantR no Error ][ BPEnteringPortal1 ] -> [ Portal:orange:right| BlockPlayer WantR ][ BPEnteringPortal1 ]
  left  [ Portal:orange:left| BlockPlayer No WantL no Error ][ BPEnteringPortal1 ] -> [ Portal:orange:left| BlockPlayer WantL ][ BPEnteringPortal1 ]
  up    [ Portal:orange:up| BlockPlayer No WantU no Error ][ BPEnteringPortal1 ] -> [ Portal:orange:up| BlockPlayer WantU ][ BPEnteringPortal1 ]
  down  [ Portal:orange:down| BlockPlayer No WantD no Error ][ BPEnteringPortal1 ] -> [ Portal:orange:down| BlockPlayer WantD ][ BPEnteringPortal1 ]



endloop




( perform the move )
[ no Portal:blue ] -> [ NoPortal1 ]
[ no Portal:orange ] -> [ NoPortal2 ]

[ Block Want ] -> sfx8


( detect up cycles , with the help of an empty spot we can solve them )
[ Pointer ] -> []
[ EmptySpot ] -> []
[ PointerEnd ] -> []
up [ Portal:up | WantU ] -> [ Portal:up | Pointer WantU ]
startloop
  up [ Pointer | Portal:down ] -> [ Pointer PointerEnd | Portal:down ]
  up [ Pointer | WantU no Pointer no Portal:down ] -> [ Pointer | WantU Pointer ]
endloop

[ PointerEnd WithCollision WantU ][ No WithCollision ] -> [ ][ EmptySpot WithCollision ] sfx4
up [ Pointer WantU WithCollision | no WithCollision ][ EmptySpot ] -> [  | WithCollision ][ EmptySpot ]
up [ Portal:up | ][ EmptySpot WithCollision ] -> [ Portal:up | WithCollision ][]
( /detect up cycles)

( detect down cycles , with the help of an empty spot we can solve them )
[ Pointer ] -> []
[ EmptySpot ] -> []
[ PointerEnd ] -> []
down [ Portal:down | WantD ] -> [ Portal:down | Pointer WantD ]
startloop
  down [ Pointer | Portal:up ] -> [ Pointer PointerEnd | Portal:up ]
  down [ Pointer | WantD no Pointer no Portal:up ] -> [ Pointer | WantD Pointer ]
endloop

[ PointerEnd WithCollision WantD ][ No WithCollision ] -> [ ][ EmptySpot WithCollision ] sfx4
down [ Pointer WantD WithCollision | no WithCollision ][ EmptySpot ] -> [  | WithCollision ][ EmptySpot ]
down [ Portal:down | ][ EmptySpot WithCollision ] -> [ Portal:down | WithCollision ][]
( /detect down cycles)

( detect left cycles , with the help of an empty spot we can solve them )
[ Pointer ] -> []
[ EmptySpot ] -> []
[ PointerEnd ] -> []
left [ Portal:left | WantL ] -> [ Portal:left | Pointer WantL ]
startloop
  left [ Pointer | Portal:right ] -> [ Pointer PointerEnd | Portal:right ]
  left [ Pointer | WantL no Pointer no Portal:right ] -> [ Pointer | WantL Pointer ]
endloop

[ PointerEnd WithCollision WantL ][ No WithCollision ] -> [ ][ EmptySpot WithCollision ] sfx4
left [ Pointer WantL WithCollision | no WithCollision ][ EmptySpot ] -> [  | WithCollision ][ EmptySpot ]
left [ Portal:left | ][ EmptySpot WithCollision ] -> [ Portal:left | WithCollision ][]
( /detect left cycles)

( detect right cycles , with the help of an empty spot we can solve them )
[ Pointer ] -> []
[ EmptySpot ] -> []
[ PointerEnd ] -> []
right [ Portal:right | WantR ] -> [ Portal:right | Pointer WantR ]
startloop
  right [ Pointer | Portal:left ] -> [ Pointer PointerEnd | Portal:left ]
  right [ Pointer | WantR no Pointer no Portal:left ] -> [ Pointer | WantR Pointer ]
endloop

[ PointerEnd WithCollision WantR ][ No WithCollision ] -> [ ][ EmptySpot WithCollision ] sfx4
right [ Pointer WantR WithCollision | no WithCollision ][ EmptySpot ] -> [  | WithCollision ][ EmptySpot ]
right [ Portal:right | ][ EmptySpot WithCollision ] -> [ Portal:right | WithCollision ][]
( /detect right cycles)
[ Pointer ] -> []
[ EmptySpot ] -> []
[ PointerEnd ] -> []



startloop


  (Move with or without portal )
  left  [ WantL BlockPlayer Portal1OrNothing Portal2OrNothing | No WithCollision No WantL ] -> [ | BlockPlayer Portal1OrNothing Portal2OrNothing ]
  right [ WantR BlockPlayer Portal1OrNothing Portal2OrNothing | No WithCollision No WantR ] -> [ | BlockPlayer Portal1OrNothing Portal2OrNothing ]
  up    [ WantU BlockPlayer Portal1OrNothing Portal2OrNothing | No WithCollision No WantU ] -> [ | BlockPlayer Portal1OrNothing Portal2OrNothing ]
  down  [ WantD BlockPlayer Portal1OrNothing Portal2OrNothing | No WithCollision No WantD ] -> [ | BlockPlayer Portal1OrNothing Portal2OrNothing ]
 
 
  (Teleportation! )    
  (1RR)
  right [ Portal:blue:right| WantL BlockPlayer BPEnteringPortal1 ][ PortalExit:orange:right No WithCollision No WantR ] -> [ Portal:blue:right| ][ PortalExit:orange:right BlockPlayer RotateRR ]
  (1RL)
  right [ Portal:blue:right| WantL BlockPlayer BPEnteringPortal1 ][ PortalExit:orange:left No WithCollision No WantL ] -> [ Portal:blue:right| ][ PortalExit:orange:left BlockPlayer RotateRL ]
  (1RU)
  right [ Portal:blue:right| WantL BlockPlayer BPEnteringPortal1 ][ PortalExit:orange:up No WithCollision No WantU ] -> [ Portal:blue:right| ][ PortalExit:orange:up BlockPlayer RotateRU ]
  (1RD)
  right [ Portal:blue:right| WantL BlockPlayer BPEnteringPortal1 ][ PortalExit:orange:down No WithCollision No WantD ] -> [ Portal:blue:right| ][ PortalExit:orange:down BlockPlayer RotateRD ]

  (1LR)
  left  [ Portal:blue:left| WantR BlockPlayer BPEnteringPortal1 ][ PortalExit:orange:right No WithCollision No WantR ] -> [ Portal:blue:left| ][ PortalExit:orange:right BlockPlayer RotateLR ]
  (1LL)
  left  [ Portal:blue:left| WantR BlockPlayer BPEnteringPortal1 ][ PortalExit:orange:left No WithCollision No WantL ] -> [ Portal:blue:left| ][ PortalExit:orange:left BlockPlayer RotateLL ]
  (1LU)
  left  [ Portal:blue:left| WantR BlockPlayer BPEnteringPortal1 ][ PortalExit:orange:up No WithCollision No WantU ] -> [ Portal:blue:left| ][ PortalExit:orange:up BlockPlayer RotateLU ]
  (1LD)
  left  [ Portal:blue:left| WantR BlockPlayer BPEnteringPortal1 ][ PortalExit:orange:down No WithCollision No WantD ] -> [ Portal:blue:left| ][ PortalExit:orange:down BlockPlayer RotateLD ]

  (1UR)
  up    [ Portal:blue:up| WantD BlockPlayer BPEnteringPortal1 ][ PortalExit:orange:right No WithCollision No WantR ] -> [ Portal:blue:up| ][ PortalExit:orange:right BlockPlayer RotateUR ]
  (1UL)
  up    [ Portal:blue:up| WantD BlockPlayer BPEnteringPortal1 ][ PortalExit:orange:left No WithCollision No WantL ] -> [ Portal:blue:up| ][ PortalExit:orange:left BlockPlayer RotateUL ]
  (1UU)
  up    [ Portal:blue:up| WantD BlockPlayer BPEnteringPortal1 ][ PortalExit:orange:up No WithCollision No WantU ] -> [ Portal:blue:up| ][ PortalExit:orange:up BlockPlayer RotateUU ]
  (1UD)
  up    [ Portal:blue:up| WantD BlockPlayer BPEnteringPortal1 ][ PortalExit:orange:down No WithCollision No WantD ] -> [ Portal:blue:up| ][ PortalExit:orange:down BlockPlayer RotateUD ]

  (1DR)
  down  [ Portal:blue:down| WantU BlockPlayer BPEnteringPortal1 ][ PortalExit:orange:right No WithCollision No WantR ] -> [ Portal:blue:down| ][ PortalExit:orange:right BlockPlayer RotateDR ]
  (1DL)
  down  [ Portal:blue:down| WantU BlockPlayer BPEnteringPortal1 ][ PortalExit:orange:left No WithCollision No WantL ] -> [ Portal:blue:down| ][ PortalExit:orange:left BlockPlayer RotateDL ]
  (1DU)
  down  [ Portal:blue:down| WantU BlockPlayer BPEnteringPortal1 ][ PortalExit:orange:up No WithCollision No WantU ] -> [ Portal:blue:down| ][ PortalExit:orange:up BlockPlayer RotateDU ]
  (1DD)
  down  [ Portal:blue:down| WantU BlockPlayer BPEnteringPortal1 ][ PortalExit:orange:down No WithCollision No WantD ] -> [ Portal:blue:down| ][ PortalExit:orange:down BlockPlayer RotateDD ]

  (2RR)
  right [ Portal:orange:right| WantL BlockPlayer BPEnteringPortal2 ][ PortalExit:blue:right No WithCollision No WantR ] -> [ Portal:orange:right| ][ PortalExit:blue:right BlockPlayer RotateRR ]
  (2RL)
  right [ Portal:orange:right| WantL BlockPlayer BPEnteringPortal2 ][ PortalExit:blue:left No WithCollision No WantL ] -> [ Portal:orange:right| ][ PortalExit:blue:left BlockPlayer RotateRL ]
  (2RU)
  right [ Portal:orange:right| WantL BlockPlayer BPEnteringPortal2 ][ PortalExit:blue:up No WithCollision No WantU ] -> [ Portal:orange:right| ][ PortalExit:blue:up BlockPlayer RotateRU ]
  (2RD)
  right [ Portal:orange:right| WantL BlockPlayer BPEnteringPortal2 ][ PortalExit:blue:down No WithCollision No WantD ] -> [ Portal:orange:right| ][ PortalExit:blue:down BlockPlayer RotateRD ]

  (2LR)
  left  [ Portal:orange:left| WantR BlockPlayer BPEnteringPortal2 ][ PortalExit:blue:right No WithCollision No WantR ] -> [ Portal:orange:left| ][ PortalExit:blue:right BlockPlayer RotateLR ]
  (2LL)
  left  [ Portal:orange:left| WantR BlockPlayer BPEnteringPortal2 ][ PortalExit:blue:left No WithCollision No WantL ] -> [ Portal:orange:left| ][ PortalExit:blue:left BlockPlayer RotateLL ]
  (2LU)
  left  [ Portal:orange:left| WantR BlockPlayer BPEnteringPortal2 ][ PortalExit:blue:up No WithCollision No WantU ] -> [ Portal:orange:left| ][ PortalExit:blue:up BlockPlayer RotateLU ]
  (2LD)
  left  [ Portal:orange:left| WantR BlockPlayer BPEnteringPortal2 ][ PortalExit:blue:down No WithCollision No WantD ] -> [ Portal:orange:left| ][ PortalExit:blue:down BlockPlayer RotateLD ]

  (2UR)
  up    [ Portal:orange:up| WantD BlockPlayer BPEnteringPortal2 ][ PortalExit:blue:right No WithCollision No WantR ] -> [ Portal:orange:up| ][ PortalExit:blue:right BlockPlayer RotateUR ]
  (2UL)
  up    [ Portal:orange:up| WantD BlockPlayer BPEnteringPortal2 ][ PortalExit:blue:left No WithCollision No WantL ] -> [ Portal:orange:up| ][ PortalExit:blue:left BlockPlayer RotateUL ]
  (2UU)
  up    [ Portal:orange:up| WantD BlockPlayer BPEnteringPortal2 ][ PortalExit:blue:up No WithCollision No WantU ] -> [ Portal:orange:up| ][ PortalExit:blue:up BlockPlayer RotateUU ]
  (2UD)
  up    [ Portal:orange:up| WantD BlockPlayer BPEnteringPortal2 ][ PortalExit:blue:down No WithCollision No WantD ] -> [ Portal:orange:up| ][ PortalExit:blue:down BlockPlayer RotateUD ]

  (2DR)
  down  [ Portal:orange:down| WantU BlockPlayer BPEnteringPortal2 ][ PortalExit:blue:right No WithCollision No WantR ] -> [ Portal:orange:down| ][ PortalExit:blue:right BlockPlayer RotateDR ]
  (2DL)
  down  [ Portal:orange:down| WantU BlockPlayer BPEnteringPortal2 ][ PortalExit:blue:left No WithCollision No WantL ] -> [ Portal:orange:down| ][ PortalExit:blue:left BlockPlayer RotateDL ]
  (2DU)
  down  [ Portal:orange:down| WantU BlockPlayer BPEnteringPortal2 ][ PortalExit:blue:up No WithCollision No WantU ] -> [ Portal:orange:down|  ][ PortalExit:blue:up BlockPlayer RotateDU ]
  (2DD)
  down  [ Portal:orange:down| WantU BlockPlayer BPEnteringPortal2 ][ PortalExit:blue:down No WithCollision No WantD ] -> [ Portal:orange:down| ][ PortalExit:blue:down BlockPlayer RotateDD ]
 
endloop


(remove these things again)
[ RigidEnteringPortal1 ] -> []
[ RigidEnteringPortal2 ] -> []
[ RigidLeavingPortal1 ] -> []
[ RigidLeavingPortal2 ] -> []
[ BPEnteringPortal1 ] -> []
[ BPEnteringPortal2 ] -> []
[ BPLeavingPortal1 ] -> []
[ BPLeavingPortal2 ] -> []

[ NoPortal1 ] -> []
[ NoPortal2 ] -> []


(END OF MOVEMENT LOGIC
===========================================================================================
)


( Rotation ! )
 [ Rotate ] -> [ Rotate ] sfx4 (a bit of a hack, but this is always created after a teleportation)

 color [ RotateCNW  Block:__:__:__:_R:color] -> [  Block:__:_D:__:__:color ]
 color [ RotateCNW  Block:__:_D:__:__:color] -> [  Block:__:__:_L:__:color ]
 color [ RotateCNW  Block:__:__:_L:__:color] -> [  Block:_U:__:__:__:color ]
 color [ RotateCNW  Block:_U:__:__:__:color] -> [  Block:__:__:__:_R:color ]
 
 directions [ RotateCNW Player:blue:directions   ]-> [ Player:blue:rotate_directions_cw ]
 directions [ RotateCNW Player:orange:directions ]-> [ Player:orange:rotate_directions_cw ]


 color [ RotateCW   Block:__:_D:__:__:color] -> [  Block:__:__:__:_R:color ]
 color [ RotateCW   Block:__:__:_L:__:color] -> [  Block:__:_D:__:__:color ]
 color [ RotateCW   Block:_U:__:__:__:color] -> [  Block:__:__:_L:__:color ]
 color [ RotateCW   Block:__:__:__:_R:color] -> [  Block:_U:__:__:__:color ]

 directions [ RotateCW Player:blue:directions   ]-> [ Player:blue:rotate_directions_cnw ]
 directions [ RotateCW Player:orange:directions ]-> [ Player:orange:rotate_directions_cnw ]

 color [ RotatePerpendicular   Block:_U:_D:__:__:color] -> [  Block:__:__:_L:_R:color ]
 color [ RotatePerpendicular   Block:__:__:_L:_R:color] -> [  Block:_U:_D:__:__:color ]
 
 vertical_tag   [ FlipVertical Player:blue:vertical_tag   ]-> [ Player:blue:flip_vertical ]
 vertical_tag   [ FlipVertical Player:orange:vertical_tag ]-> [ Player:orange:flip_vertical ]


 color [ FlipVertical    Block:__:_D:__:__:color ] -> [  Block:_U:__:__:__:color ]
 color [ FlipVertical    Block:_U:__:__:__:color ] -> [  Block:__:_D:__:__:color ]
 color [ FlipHorizontal  Block:__:__:__:_R:color ] -> [  Block:__:__:_L:__:color ]
 color [ FlipHorizontal  Block:__:__:_L:__:color ] -> [  Block:__:__:__:_R:color ]
 
 horizontal_tag [ FlipHorizontal Player:blue:horizontal_tag ] -> [ Player:blue:flip_horizontal ]
 horizontal_tag [ FlipHorizontal Player:orange:horizontal_tag ] -> [ Player:orange:flip_horizontal ]
 

 [ Rotate ] -> [  ] 


( detect broken rigids )
[ OkRigid ] -> []
[ With:> | With:< ] -> [ With:> OkRigid:> | With:< OkRigid:< ]
[ With:> | ActivePortal:< ] -> [ With:> | action ActivePortal:< ]
[ action Portal ][ stationary Portal ] -> [ stationary Portal  ][ stationary Portal ]

[ With:> | action ActivePortal:< ] -> [ With:> OkRigid:> | ActivePortal:< ]


[ With:> no OkRigid:> ] -> sfx5
( [ With:> no OkRigid:> ] -> cancel )




([ Error ] -> cancel sfx5)
([ Error ] -> [ ErrorSprite ])
[ Error ] -> [ ]

[ Want ] -> sfx5
([ Want ] -> cancel )



( Create Light Bridge )
[ LightBridgeStarter:> no LightBridge:> no LightBridge:< ] -> [ LightBridgeStarter:> LightBridge:> ]



[ LightBridgeEnteringPortal:portal_color ] -> [ ]


( Spread light Bridge, including Portals )
startloop
   [ LightBridge:> | no LightBridge:> no LightBridge:< no DarkOrWhiteWall no Portal:< ] -> [ LightBridge:> | LightBridge:> ]
   
   
   portal_color [ LightBridge:> no LightBridgeEnteringPortal:portal_color | Portal:portal_color:< ] -> [ LightBridge:> LightBridgeEnteringPortal:portal_color | Portal:portal_color:< ]
   
   portal_color [ LightBridgeEnteringPortal:toggle_portal_color ][ Portal:portal_color:> | no LightBridge:< no LightBridge:> no DarkOrWhiteWall ] -> [ LightBridgeEnteringPortal:toggle_portal_color ][ Portal:portal_color:> | LightBridge:> ]


endloop

(start sink)
startloop
   [ Block Water no LightBridge ] -> [ Block MightSink Water ]
   
   ( contiguous to might sink, spread it )
   [ MightSink With:> | With:< no MightSink ] -> [ MightSink With:> | With:< MightSink ]
   
endloop

startloop
   [ MightSink LightBridge ] -> [ Error MightSink LightBridge ]
   [ MightSink no Water ] -> [ Error MightSink ]

   ( contiguous to error, spread it )
   [ MightSink With:> Error | With:< MightSink no Error ] -> [ MightSink With:> Error | With:< MightSink Error ]
   
   (going through a portal, prevent sinking)
   [ MightSink With:> | ActivePortal:< ] -> [ MightSink With:> Error | ActivePortal:< ]
   
endloop

[ MightSink Error ] -> [ ]
[ Error ] -> []

[ MightSink LightBridge ] -> [ LightBridge ]

(Sink the blocks, turning them into bridges:)
color uu dd ll rr [ MightSink Block:uu:dd:ll:rr:color Water ] -> [ Bridge:uu:dd:ll:rr:color ] sfx6


[ Portal  no WithWhite ] -> [  ]


[ SinkingPlayerSprite3 ] -> [ ]
[ SinkingPlayerSprite2 ] -> [ SinkingPlayerSprite3 ] again
[ SinkingPlayerSprite1 ] -> [ SinkingPlayerSprite2 ] again
[ RealPlayer Water no LightBridge ] -> [ Water SinkingPlayerSprite1 MissingPlayer ] sfx7 again

   



( cleanup errors )

[ PostError ] -> []
[ Error ] -> []



(delete portal helpers, see ya next turn)
[ PortalExit ] -> [ ]
[ ActivePortal ] -> []


( Decorative rules )


[ Portal1Sprite no Portal:blue ] -> []
[ Portal1RS4 ] -> [  ]
[ Portal1LS4 ] -> [  ]
[ Portal1US4 ] -> [  ]
[ Portal1DS4 ] -> [  ]
[ Portal1RS3 ] -> [ Portal1RS4 ]
[ Portal1LS3 ] -> [ Portal1LS4 ]
[ Portal1US3 ] -> [ Portal1US4 ]
[ Portal1DS3 ] -> [ Portal1DS4 ]
[ Portal1RS2 ] -> [ Portal1RS3 ]
[ Portal1LS2 ] -> [ Portal1LS3 ]
[ Portal1US2 ] -> [ Portal1US3 ]
[ Portal1DS2 ] -> [ Portal1DS3 ]
[ Portal1RS1 ] -> [ Portal1RS2 ]
[ Portal1LS1 ] -> [ Portal1LS2 ]
[ Portal1US1 ] -> [ Portal1US2 ]
[ Portal1DS1 ] -> [ Portal1DS2 ]
[ Portal:blue:right no Portal1Sprite ] -> [ Portal:blue:right Portal1RS1 ]
[ Portal:blue:left no Portal1Sprite ] -> [ Portal:blue:left Portal1LS1 ]
[ Portal:blue:up no Portal1Sprite ] -> [ Portal:blue:up Portal1US1 ]
[ Portal:blue:down no Portal1Sprite ] -> [ Portal:blue:down Portal1DS1 ]

[ Portal2Sprite no Portal:orange ] -> []
[ Portal2RS4 ] -> [  ]
[ Portal2LS4 ] -> [  ]
[ Portal2US4 ] -> [  ]
[ Portal2DS4 ] -> [  ]
[ Portal2RS3 ] -> [ Portal2RS4 ]
[ Portal2LS3 ] -> [ Portal2LS4 ]
[ Portal2US3 ] -> [ Portal2US4 ]
[ Portal2DS3 ] -> [ Portal2DS4 ]
[ Portal2RS2 ] -> [ Portal2RS3 ]
[ Portal2LS2 ] -> [ Portal2LS3 ]
[ Portal2US2 ] -> [ Portal2US3 ]
[ Portal2DS2 ] -> [ Portal2DS3 ]
[ Portal2RS1 ] -> [ Portal2RS2 ]
[ Portal2LS1 ] -> [ Portal2LS2 ]
[ Portal2US1 ] -> [ Portal2US2 ]
[ Portal2DS1 ] -> [ Portal2DS2 ]
[ Portal:orange:right no Portal2Sprite ] -> [ Portal:orange:right Portal2RS1 ]
[ Portal:orange:left no Portal2Sprite ] -> [ Portal:orange:left Portal2LS1 ]
[ Portal:orange:up no Portal2Sprite ] -> [ Portal:orange:up Portal2US1 ]
[ Portal:orange:down no Portal2Sprite ] -> [ Portal:orange:down Portal2DS1 ]


[ DarkWall no DarkWallSprite ] -> [ DarkWall random DarkWallSprite ]
[ RealTimeTick ][ WaterSprite ] -> [RealTimeTick][]
[ RealTimeTick ][ Water no WaterSprite ] -> [RealTimeTick][ Water random WaterSprite ]


up    [ no HasCellU | ] -> [ HasCellU | ]
down  [ no HasCellD | ] -> [ HasCellD | ]
right [ no HasCellR | ] -> [ HasCellR | ]
left  [ no HasCellL | ] -> [ HasCellL | ]

up   [ DarkWall no HasCellU no DarkWallTop  ] -> [ DarkWall DarkWallTop ]
left [ DarkWall no HasCellL no DarkWallLeft ] -> [ DarkWall DarkWallLeft ]
right[ DarkWall no HasCellR no DarkWallRight ] -> [ DarkWall DarkWallRight ]
down [ DarkWall no HasCellD no DarkWallBottom] -> [ DarkWall DarkWallBottom ]


up   [ DarkWall no DarkWallTop  | no DarkOrWhiteWall ] -> [ DarkWall DarkWallTop |]
left [ DarkWall no DarkWallLeft | no DarkOrWhiteWall ] -> [ DarkWall DarkWallLeft|]
right[ DarkWall no DarkWallRight| no DarkOrWhiteWall ] -> [ DarkWall DarkWallRight|]
down [ DarkWall no DarkWallBottom | no DarkOrWhiteWall ] -> [ DarkWall DarkWallBottom|]

up   [ Wall no WallTop:white  | no DarkOrWhiteWall ] -> [ Wall WallTop:white |]
left [ Wall no WallLeft:white | no DarkOrWhiteWall ] -> [ Wall WallLeft:white|]
right[ Wall no WallRight:white| no DarkOrWhiteWall ] -> [ Wall WallRight:white|]
down [ Wall no WallBottom:white | no DarkOrWhiteWall ] -> [ Wall WallBottom:white|]

up   [ Wall no HasCellU ] -> [ Wall WallTop:white ]
left [ Wall no HasCellL ] -> [ Wall WallLeft:white]
right[ Wall no HasCellR ] -> [ Wall WallRight:white]
down [ Wall no HasCellD ] -> [ Wall WallBottom:white]


[ no HasCellU no  HasCellL ] -> [ CellOdd ]

startloop
  [ CellOdd  | no CellEven ] -> [ CellOdd  | CellEven ]
  [ CellEven | no CellOdd  ] -> [ CellEven | CellOdd ]
endloop

[ Wall CellOdd ] -> [ Wall WallOdd CellOdd ]



[ WallBottom:white WallTop:white WallLeft:white WallRight:white no Wallx1:white ] -> [ WallBottom:white WallTop:white WallLeft:white WallRight:white Wallx1:white ]


down  [ Wall | DarkWall ] -> [ Wall | WallSeparatorU DarkWall ]
up    [ Wall | DarkWall ] -> [ Wall | WallSeparatorD DarkWall ]
left  [ Wall | DarkWall ] -> [ Wall | WallSeparatorR DarkWall ]
right [ Wall | DarkWall ] -> [ Wall | WallSeparatorL DarkWall ]


right [ Wall        | WallBottom:white ] -> [ action Wall | WallBottom:white ]
down  [ action Wall | WallRight:white  ] -> [        Wall WallDarkCorner:white | WallRight:white  ]
[ action Wall ] -> [ Wall ]


up   [ no Water no Bridge no GroundTop   | WaterOrBridge ] -> [ GroundTop   | WaterOrBridge ]
left [ no Water no Bridge no GroundLeft  | WaterOrBridge ] -> [ GroundLeft  | WaterOrBridge ]
right[ no Water no Bridge no GroundRight | WaterOrBridge ] -> [ GroundRight | WaterOrBridge ]
down [ no Water no Bridge no GroundBottom| WaterOrBridge ] -> [ GroundBottom| WaterOrBridge ]

up   [ Target no TargetBorderTop    | Target ] -> [ Target  TargetBorderTop    | Target ]
down [ Target no TargetBorderBottom | Target ] -> [ Target  TargetBorderBottom | Target ]
left [ Target no TargetBorderLeft   | Target ] -> [ Target  TargetBorderLeft   | Target ]
right[ Target no TargetBorderRight  | Target ] -> [ Target  TargetBorderRight  | Target ]


[ Target Block no WithWhite no GoalSoundSprite ] -> [ Target Block GoalSoundSprite ] sfx10
[ Target no Block GoalSoundSprite  ] -> [ Target ] sfx9
[ Target WithWhite GoalSoundSprite ] -> [ Target WithWhite ] sfx9

directions_tag [ LightBridgeSprite:smod3:directions_tag no LightBridge:directions_tag ]  -> []

(play sfx if there are new light bridge sprites)
[ LightBridge:directions no LightBridgeSprite:smod3:directions ] -> [ LightBridge:directions ] sfx11

directions_tag [ LightBridgeSprite:s2:directions_tag ] -> [ ]
directions_tag [ LightBridgeSprite:s1:directions_tag ] -> [ LightBridgeSprite:s2:directions_tag ]
directions_tag [ LightBridgeSprite:s0:directions_tag ] -> [ LightBridgeSprite:s1:directions_tag ]

directions_tag [ LightBridge:directions_tag no LightBridgeSprite:directions_tag ] -> [ LightBridge:directions_tag  LightBridgeSprite:s0:directions_tag ]


(Adding corners is easy,  really. You just need to understand that if
 this shape exists: |_ _| , then there are four corners to add. )
color vertical horizontal [ BlockCorner:vertical:horizontal:color ] -> []

color down [ Block:uu:_D:_L:rr:color no BlockCorner:down:left:color | Block:_U:dd:_L:rr:color ] -> [ Block:uu:_D:_L:rr:color BlockCorner:down:left:color | Block:_U:dd:_L:rr:color BlockCorner:up:left:color ]

color left [ BlockCorner:down:left:color | no BlockCorner:down:right:color ] -> [ BlockCorner:down:left:color | BlockCorner:down:right:color ]

color down [ BlockCorner:down:right:color | no BlockCorner:up:right:color ] -> [ BlockCorner:down:right:color | BlockCorner:up:right:color ]

(Now we have to do it with bridges as well)

color vertical horizontal [ BridgeCorner:vertical:horizontal:color ] -> []

color down [ Bridge:uu:_D:_L:rr:color no BridgeCorner:down:left:color | Bridge:_U:dd:_L:rr:color ] -> [ Bridge:uu:_D:_L:rr:color BridgeCorner:down:left:color | Bridge:_U:dd:_L:rr:color BridgeCorner:up:left:color ]

color left [ BridgeCorner:down:left:color | no BridgeCorner:down:right:color ] -> [ BridgeCorner:down:left:color | BridgeCorner:down:right:color ]

color down [ BridgeCorner:down:right:color | no BridgeCorner:up:right:color ] -> [ BridgeCorner:down:right:color | BridgeCorner:up:right:color ]

( /Decorative Rules)

==============
WINCONDITIONS
==============

All target on Block
No WithWhite on Target
Some Player
Some Target

=======
LEVELS
=======

section 01

Message Welcome to [Redacted] Science.
Message Your clearance level is so low that we can't even share the name of the company with you.
Message Your Job is simple: Relocate the furniture to the marked locations. 'Z' to undo. 'R' to restart.


%%%%%%%%%%
%01...@.@%
%01...@..%
###..¡.%.%
####...%.%
####yyy%.%
####yy┦%.%
%%%@⅄ⱬy%.%
%@@@.....%
%%%%%%%%%%

section 02

Message At [Redacted] Science we value your safety. For that reason some restricted areas may be surrounded by safety liquid.

Message We do not recommend to submerge into the safety liquid 


%##%%%%%%%%%%%%%
%02%%%...~~.yy¡%
%02..yyy.%~~~~~%
%....y.y%%.....%
%...#...%%.yy.%%
%...#..%%%.%.%%%
%...#..~~..~..#%
%...#%%~~~~~.T#%
%@@@yy..~..~...%
%@.@.......~~~~%
%%%%%%%%%%%%%%%%


section 03

Message Friendly Reminder to all personnel: In the rare case you stumble upon any [Redacted] Science Test Equipment, you should leave it untouched and report the incident to IT.


###%%%%%%%%%
##........%%
##@@@@yyyy%%
##@.@@y..y%%
%%%.@%%....%
%%%%%y@y...%
#....y@y.03%
#..!.y@..03%
#%%%%%%%%%%%


section 04

Message Unsupervised exposure to [Redacted] Science Test Equipment might damage our furniture. It might also lead to death.

%%%%%%%%%%%%
%04....~~~%%
%04┦.~~~.~@%
%!.c.~~~.~.%
%.ñc.~~~.~.%
%..⹏.~...~.%
%....~...~.%
########%%%%



Message Rest assured that this is not a test. This is a job for humans.
Message There are no records being kept of the number of moves you are using and we don't look at any leaderboards.

section 05

%%%%%%%%%%%%%#%
%05........%.@%
%05....%~~~~.@%
%.⡧ⱬⱬℌ┦~~~~~.@%
%.c⅄☥cc~~~~~.@%
%.⸔ⱬⱬac~~~~~.%%
%.....⹏~~~~~~~%
%...!..~~~~~~~%
%###########%%%

Message If this was a test, I would have no reason to lie about it. Therefore, this is not a test.

section 06
%%%%%%%%%%%%#%%%%%
%#....~~.......06%
%%ñññ.~~.......06%
%%....~~~~~~~~~~~%
%%%%%~~~~~~~~~...%
%.....~~~.%%~..y.%
%.~~~!~~~.%%..yy%%
%.~~@.~~..#%.%#%%%
%.~~~.~~%..yy.%%h%
%.....~~%%.......%
%%%%%#%%%%%%%%%%%%

section 07

Message What findings could there possibly be made from a test like this? "Subject can push large objects" "Subject understands object rotation" Statements dreamed up by the utterly deranged.

%%%%%%%%%%%%%
%%......%%%%%
%%......~...%
%..%y%..~.~@%
#.y.y.~.~~~%%
#...y.~~~.~%%
#...y.~.07~%%
#.....~!07~%%
#########%%%%

section 08

Message Your Job is to cover all marked areas with furniture that have the [Redacted] Science Design Department-Approved Yellow Color. That's the exact job description.

Message Just imagine just how many more tasks a Test Subject would have to do instead. Aren't you glad that your job is much simpler and mundane?

#############
#.#.#...#...#
#.....#.##@@#
#.%.yyy..@@.#
#.%%..y..08##
#..yy.y!#08.#
##yy..y.###.#
#####@@@#@@@#
#############


section 09

Message Pieces of Furniture that are not in [Redacted] Science Interior Design Department-approved Yellow should be ignored.

%#######%%%%%%%%
%09....@~~~~~~.#
%09.....~~~~~~.#
%.......~~~~~~~%
%%ww%%..~~~~~~~%
#....%..~~~~~~~%
#....%..~~~~~~~%
#~~~~%..~~~....%
#..!.%..~~~.y..%
#%%%%%%%%%%####%


section 10

Message Our Scientists are busy making important discoveries such as solid light. They don't have time to be moving furniture around.
%%%%%%#%%%%
%10↓......%
%10.......%
%~~~~~~~~~%
%~~~~~~~~~%
%~~~~~~~~~%
%...%%.y.%%
%@!..%.yy%%
%@@..%...%%
%%%##%%#%%%


section 11

Message * Reviewing Past Performance Records *

Message The Previous employee in your role got Terminated because they were taking too long on moving the furniture for a certain chamber.

Message The records say: "Employee too easily distracted by bright lights". "Employee displays an inability to combine recently-learned concepts."

%%%%%%%%%%%%%%%%
%@@............%
%@.............%
%~~~~~~~~~~~~~~%
%~~~~~~~~~~~~~~%
%~~~~~~~~~~~~~~%
#....ŧ%#....yy.%
#11.!.%#.ww.y..%
#11...%%%%%%%%%%
######%%%%%%%%%%

section 12

Message You appear to be taking quite some time to move a small piece of furniture.

Message Couldn't you be more efficient? If you could do more things at once, that'd make you a much more useful employee.


%%%%%%%%%%%%%%%%%%%
%..%%%%.....%%%%..%
%..YY.%.YYY.%.YY..%
%..Y..%..Y..%..Y..%
%...@@%.@@@.%@@...%
%%%%@%%%%@%%%%@%%%%
%.................%
%12......!........%
%12.....WW}.......%
###################

section 13

Message Maybe what you could do is become more versatile? It's so tragic humans couldn't do more with less.

%%%%%%%%%%%%%%
%13.....~~~~~%
%13.!...~~~~~%
%......%.Y.~~%
%.W.W..%...~~%
%.WWWWW.~~~~@%
%.......~~~~.%
%%%%%%%%%%%%%%


section someoldone



Message Just because you are not being tested doesn't mean your should slack off.

Message As unlikely as it may appear, these chambers took a lot of time to design.


section old09

%%%%%%%%%%%%%%
%............%
%.“»%%%%%%%%@%
%........%%%%%
%...%%..%%...%
%.ñ........09%
%%%%%.%.%%.09%
%%%%..%%%%%%%%
%%%%.!%%%%%%%%
%%%%..%%%%%%%%
%%%%%%%%%%%%%%

section old04

%%%%%%
%%%@%%
%04@@%
%04@@%
%~~..%
%..!~%
%yy..%
#yy.ñ%
#....%
%%%%#%



section old10

%%%%%%%%%%%%%%%%%%
%%%%%%.⅄☥.~....10%
%%%%%%..%.%....10%
%....%....%%%%%~%%
%....%.%%.%%%%%.%%
#@@..~.%~~~...!..%
%....~~~~~~..ñ...%
%~~~~~~~~~~..“·».%
%~~~~~~~~~~..“·».%
%%%%%%%%%%%%#%%%%%





section old11

Message As they say, you should aim to have as few mechanics as possible. That's why we've replaced all of the mechanics with repair robots.

%#%%%%%%%%%%%%%%%%
%@ñ┦%.....~~~....%
%..¢%.....~~~..@.%
%~~c%....┦~~~.@@.%
%!.c%.“»⅄a~~~.@@.%
%.⅄a%.“»⡧☥~~~.@..%
%..~~.“»⹏.~~~..11%
%%.~%.....~~~..11%
%%%%%%%%%%%%%%%%%%

Message Reminder to employees: Do not fill safety liquid pools with unused furniture unless absolutely necesssary.

section old12

%%%%%%%##%%%%%%%%
%%@%%%%~~.....12%
%%@%%%%~~..@@.12%
%%.....~~..@....%
%%.....~~.......%
%%~~~~~~~~~~~~~~%
%...............%
%..⡧☥#.!.“·»....%
%..⹏.%...⅄ⱬⱬⱬℌ..%
%....%.......⹏..%
%%%%%%%%%%%%%%%%%

Message [Redacted] Science would like to thank our surviving employees. Your efforts are noted.

section 13

%%%%%%%%%%%%%%%
%......~~~....%
%......~~~....%
%.“»!┦.~~~..@.%
%.“»⅄a.~~~.@@.%
%.“»┦..~~~.@@.%
%...⸔☥.~~~.@..%
%......~~~..13%
%......~~~..13%
%%%%%%%%%%%%%%%

Message Congratulations on making this far. If this was a test, the results would be very satisfactory.

section 14

%%%%%%%%%%%%%%%
%%%%%~~~......%
%%%%%~%~%%┦...%
%%%..~.~.%c...%
%%%.ñ~.~@%⹏.14%
%%%..~~~.%┦.14%
%%%%%~~~~%c..%%
%...#...#.⹏..%%
%.}}.~~.%...%%%
%.......%.┦.%%%
%%~~~~~~%.c.%%%
%%~~~~~~..⹏.%%%
%%...!.~..#.%%%
%%%%%%#%%%%%%%%




Message [ You've finished placing the furniture. Our test chambers are finally ready. ]

Message Good Job, Human.

section 15

%%%%%%%%%%%%%%%%%%%%
%..................%
%..................%
%.....ñ.ñ.ñ........%
%.....µ.µ.µ........%
%....┦\·”·æ┦.......%
%....⸔ⱬⱬⱬⱬⱬa.......%
%.............!....%
%..................%
%................66%
%................00%
%%%%%%%%%%%%%%%%%%%%



(

level_select_point (08)

Message joke

%%%%%%%%%%%%%%%
%o⑧.....~~@@@~%
%ö⑻⡧ⱬⱬⱬⱬℌ~..@~%
%..c....c~~~~~%
%..c....c~~~~~%
%..c....⹏~~~~~%
%..c....%.ñ~~~%
%..⹏.!..%..~~~%
%........~~~~@%
%.“···»..~~~~.%
%........~~~~~%
%........%%%%%%
%%%%%%%%%%%%%%%


Message sadsg

%%%%%%%%%%%%%%%
%.......~~@@@~%
%.......~~..@~%
%⡧ⱬⱬⱬⱬℌ.~~~~~~%
%c....c.~~~~~~%
%c....c.~~~~~~%
%c....⹏.%..o⑦~%
%c......%.ñö⑺~%
%⹏.“···».~~~~@%
%........~~~~.%
%...!....~~~~~%
%........~~~~~%
%%%%%%%%%%%%%%%


Message [ End of playtest, big congrats ]


(



#################
~...............#
~.......⡧ⱬℌ..!..#
~.......⹏.⹏.....#
~...#############
~...#############
~...#############
~............@@@#
~............@.@#
#################

#################
#...............#
#..⅄☥...⡧ⱬℌ⅄ⱬℌ..#
#....!..⹏.⹏..⸔6.#
#...####......c.#
#...###⅄☥..⅄ⱬⱬa.#
#...###..###.####
#............@@@#
#............@.@#
#################


#################
#...............#
#..⅄☥...⡧ⱬℌ.....#
#.......⹏.⹏.....#
#...####........#
#...######..!...#
#...######.#.####
#............@@@#
#............@.@#
@################





###################
############.....##
#..........#.....@#
#.!........#~~~~.##
#...#.⡧ⱬⱬℌ..~~~~..#
#.....c⅄☥c..~~~~..#
#...ñ.⸔ⱬⱬa..~~~~.##
#...............~##
#...............~##
#...............###
###################

#################
#...............#
#.......⡧ⱬℌ.....#
#.......⹏.⹏.....#
#...######......#
#...######..!...#
#...#############
#............@@@#
#............@.@#
#################







#################
#@@@@...##......#
#@..@...........#
#........⡧☥.⡧ⱬⱬℌ#
#....⡧ⱬℌ.c..⹏..⹏#
#....c!dⱬa......#
#####⸔ⱬa........#
#####.......#...#
#####...........#
#################



#################
#...............#
#...........@...#
#...............#
#....⡧ⱬℌ........#
#....c!c........#
#....⸔ⱬa........#
#...............#
#...............#
#################


#################
#...............#
#.......⡧ⱬℌ.....#
#.......⹏.⹏.....#
#...######......#
#...######..!...#
#...#############
#............@@@#
#............@.@#
#################


#################
#...............#
#.!..⅄ⱬ╎☥..ñ....#
#...#..⹏........#
#....⅄ℌ⅄ⱬ☥⅄☥..~.#
#..⡧ⱬℌ⸔ℌ┦.....~.#
#..⹏.⹏4⹏csg..~~.#
#.....⸔╎apn.~~~.#
#......⹏..~~~~~.#
#@..............#
#################
)
